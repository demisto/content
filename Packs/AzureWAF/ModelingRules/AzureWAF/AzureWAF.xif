/*
Handles Azure WAF Common Fields
*/
[RULE: msft_azure_resourcelogs_common_fields]
alter
    xdm.target.resource.id = resourceId,
	xdm.event.type = if(
		Category = "FrontDoorAccessLog", "Front Door Access Log",
		Category = "FrontDoorWebApplicationFirewallLog", "Front Door Web Application Firewall Log",
        category = "ApplicationGatewayFirewallLog", "Application Gateway Firewall Log",
		category = "ApplicationGatewayAccessLog", "Application Gateway Access Log"),
	xdm.source.cloud.project_id = coalesce(SubscriptionId, arrayindex(regextract(resourceId, "/SUBSCRIPTIONS/([^\/]+)"), 0), tenantId),
	xdm.event.original_event_type = coalesce(category,Category),
	xdm.event.operation_sub_type = operationName,	
	xdm.session_context_id = correlationId,
	xdm.source.user.username = to_string(identity),
	xdm.event.log_level = if(level ~= "Information", XDM_CONST.LOG_LEVEL_INFORMATIONAL, level = "Warning", XDM_CONST.LOG_LEVEL_WARNING, level = "Error", XDM_CONST.LOG_LEVEL_ERROR, level = "Critical", XDM_CONST.LOG_LEVEL_CRITICAL, level = null, null, to_string(level)),
	xdm.source.cloud.region = location,
	xdm.source.cloud.provider = XDM_CONST.CLOUD_PROVIDER_AZURE;



/*
Handles Front Door Common Fields
FrontDoorAccessLog and FrontDoorWebApplicationFirewallLog
*/
[RULE: front_door_common_fields]
filter Category in ("FrontDoorAccessLog", "FrontDoorWebApplicationFirewallLog")
| alter CallerIPAddress = if(len(CallerIPAddress) = 0,null, CallerIPAddress )
| alter tmp_ip_address = coalesce(CallerIPAddress, clientIP_s) 
| alter
    source_ipv4 = arrayindex(regextract(tmp_ip_address, "(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"),0),
    source_ipv6 = arrayindex(regextract(tmp_ip_address, "([a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5})"),0),
    source_socket_ipv4 = regextract(socketIP_s, "(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"),
    source_socket_ipv6 = regextract(socketIP_s, "([a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5})"),
    clientPort_s = to_integer(clientPort_s),
    clientPort_d = to_integer(clientPort_d)

| alter
    xdm.event.id = trackingReference_s,
    xdm.target.url = requestUri_s, 
    xdm.target.resource.name = Resource,
    xdm.target.resource.parent_id = ResourceGroup ,
    xdm.target.resource.type = ResourceType ,
    xdm.source.port = coalesce(clientPort_d , clientPort_s),
    xdm.source.ipv4 = source_ipv4 ,
    xdm.source.ipv6 = source_ipv6,
    xdm.source.host.ipv4_addresses = source_socket_ipv4,
    xdm.source.host.ipv6_addresses = source_socket_ipv6;



/*
Handles Front Door WAF Logs
*/
[RULE: front_door_waf_log]
 alter
    xdm.observer.action	= action_s,
    xdm.target.domain = host_s,
    xdm.network.rule = ruleName_s,
    xdm.event.description = details_data_s;



/*
Handle Frond Door Access Logs
*/
[RULE: front_door_access_log]
 alter
    UserAgent = if(len(UserAgent ) = 0, null, UserAgent ),
    tmp_tls_version = if(originCryptProtocol_s = "N/A", securityProtocol_s, originCryptProtocol_s ),
    target_ipv4 = arrayindex(regextract(originIp_s , "(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"),0),
    target_ipv6 = arrayindex(regextract(originIp_s, "([a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5}[a-fA-F0-9\:]{1,5})"),0),
    trim_domain = replex(domain_s , ":\d+",""),
    error_information_outcome = if(errorInfo_s = "NoError", XDM_CONST.OUTCOME_SUCCESS, XDM_CONST.OUTCOME_FAILED),
    error_information_outcome_reason= if(
    errorInfo_s ="NoError", "NoError: No error was found.",
    errorInfo_s ="CertificateError","CertificateError: Generic SSL certificate error.",
    errorInfo_s ="CertificateNameCheckFailed","CertificateNameCheckFailed: The host name in the SSL certificate is invalid or doesn't match the requested URL.",
    errorInfo_s ="ClientDisconnected","ClientDisconnected: The request failed because of a client network connection issue.",
    errorInfo_s ="ClientGeoBlocked","ClientGeoBlocked: The client was blocked due to the geographical location of the IP address.",
    errorInfo_s ="UnspecifiedClientError","UnspecifiedClientError: Generic client error.",
    errorInfo_s ="InvalidRequest","InvalidRequest: Invalid request. This response indicates a malformed header, body, or URL.",
    errorInfo_s ="DNSFailure","DNSFailure: A failure occurred during DNS resolution.",
    errorInfo_s ="DNSTimeout","DNSTimeout: The DNS query to resolve the origin IP address timed out.",
    errorInfo_s ="DNSNameNotResolved","DNSNameNotResolved: The server name or address couldn't be resolved.",
    errorInfo_s ="OriginConnectionAborted","OriginConnectionAborted: The connection with the origin was disconnected abnormally.",
    errorInfo_s ="OriginConnectionError","OriginConnectionError: Generic origin connection error.",
    errorInfo_s ="OriginConnectionRefused","OriginConnectionRefused: The connection with the origin wasn't established.",
    errorInfo_s ="OriginError","OriginError: Generic origin error.",
    errorInfo_s ="ResponseHeaderTooBig","ResponseHeaderTooBig: The origin returned a too large of a response header.",
    errorInfo_s ="OriginInvalidResponse","OriginInvalidResponse: The origin returned an invalid or unrecognized response.",
    errorInfo_s ="OriginTimeout","OriginTimeout: The time-out period for the origin request expired.",
    errorInfo_s ="ResponseHeaderTooBig","ResponseHeaderTooBig: The origin returned a too large of a response header.",
    errorInfo_s ="RestrictedIP","RestrictedIP: The request was blocked because of restricted IP address.",
    errorInfo_s ="SSLHandshakeError","SSLHandshakeError: Azure Front Door was unable to establish a connection with the origin because of an SSL handshake failure.",
    errorInfo_s ="SSLInvalidRootCA","SSLInvalidRootCA: The root certification authority's certificate was invalid.",
    errorInfo_s ="SSLInvalidCipher","SSLInvalidCipher: The HTTPS connection was established using an invalid cipher.",
    errorInfo_s ="OriginConnectionAborted","OriginConnectionAborted: The connection with the origin was disconnected abnormally.",
    errorInfo_s ="OriginConnectionRefused","OriginConnectionRefused: The connection with the origin wasn't established.",
    errorInfo_s ="UnspecifiedError","UnspecifiedError: An error occurred that did not fit in any of the errors in the table."),
    check_response_code = coalesce(to_integer(httpStatusCode_s) , httpStatusCode_d ,to_integer(httpStatusDetails_s))

| alter 
    set_response_code = if(check_response_code = 0,"The request to the origin timed out", check_response_code = 499,"The client closed the connection", check_response_code = 100, XDM_CONST.HTTP_RSP_CODE_CONTINUE, check_response_code = 101, XDM_CONST.HTTP_RSP_CODE_SWITCHING_PROTOCOLS, check_response_code = 102, XDM_CONST.HTTP_RSP_CODE_PROCESSING, check_response_code = 103, XDM_CONST.HTTP_RSP_CODE_EARLY_HINTS, check_response_code = 200, XDM_CONST.HTTP_RSP_CODE_OK, check_response_code = 201, XDM_CONST.HTTP_RSP_CODE_CREATED, check_response_code = 202, XDM_CONST.HTTP_RSP_CODE_ACCEPTED, check_response_code = 203, XDM_CONST.HTTP_RSP_CODE_NON__AUTHORITATIVE_INFORMATION, check_response_code = 204, XDM_CONST.HTTP_RSP_CODE_NO_CONTENT, check_response_code = 205, XDM_CONST.HTTP_RSP_CODE_RESET_CONTENT, check_response_code = 206, XDM_CONST.HTTP_RSP_CODE_PARTIAL_CONTENT, check_response_code = 207, XDM_CONST.HTTP_RSP_CODE_MULTI__STATUS, check_response_code = 208, XDM_CONST.HTTP_RSP_CODE_ALREADY_REPORTED, check_response_code = 226, XDM_CONST.HTTP_RSP_CODE_IM_USED, check_response_code = 300, XDM_CONST.HTTP_RSP_CODE_MULTIPLE_CHOICES, check_response_code = 301, XDM_CONST.HTTP_RSP_CODE_MOVED_PERMANENTLY, check_response_code = 302, XDM_CONST.HTTP_RSP_CODE_FOUND, check_response_code = 303, XDM_CONST.HTTP_RSP_CODE_SEE_OTHER, check_response_code = 304, XDM_CONST.HTTP_RSP_CODE_NOT_MODIFIED, check_response_code = 305, XDM_CONST.HTTP_RSP_CODE_USE_PROXY, check_response_code = 307, XDM_CONST.HTTP_RSP_CODE_TEMPORARY_REDIRECT, check_response_code = 308, XDM_CONST.HTTP_RSP_CODE_PERMANENT_REDIRECT, check_response_code = 400, XDM_CONST.HTTP_RSP_CODE_BAD_REQUEST, check_response_code = 401, XDM_CONST.HTTP_RSP_CODE_UNAUTHORIZED, check_response_code = 402, XDM_CONST.HTTP_RSP_CODE_PAYMENT_REQUIRED, check_response_code = 403, XDM_CONST.HTTP_RSP_CODE_FORBIDDEN, check_response_code = 404, XDM_CONST.HTTP_RSP_CODE_NOT_FOUND, check_response_code = 405, XDM_CONST.HTTP_RSP_CODE_METHOD_NOT_ALLOWED, check_response_code = 406, XDM_CONST.HTTP_RSP_CODE_NOT_ACCEPTABLE, check_response_code = 407, XDM_CONST.HTTP_RSP_CODE_PROXY_AUTHENTICATION_REQUIRED, check_response_code = 408, XDM_CONST.HTTP_RSP_CODE_REQUEST_TIMEOUT, check_response_code = 409, XDM_CONST.HTTP_RSP_CODE_CONFLICT, check_response_code = 410, XDM_CONST.HTTP_RSP_CODE_GONE, check_response_code = 411, XDM_CONST.HTTP_RSP_CODE_LENGTH_REQUIRED, check_response_code = 412, XDM_CONST.HTTP_RSP_CODE_PRECONDITION_FAILED, check_response_code = 413, XDM_CONST.HTTP_RSP_CODE_CONTENT_TOO_LARGE, check_response_code = 414, XDM_CONST.HTTP_RSP_CODE_URI_TOO_LONG, check_response_code = 415, XDM_CONST.HTTP_RSP_CODE_UNSUPPORTED_MEDIA_TYPE, check_response_code = 416, XDM_CONST.HTTP_RSP_CODE_RANGE_NOT_SATISFIABLE, check_response_code = 417, XDM_CONST.HTTP_RSP_CODE_EXPECTATION_FAILED, check_response_code = 421, XDM_CONST.HTTP_RSP_CODE_MISDIRECTED_REQUEST, check_response_code = 422, XDM_CONST.HTTP_RSP_CODE_UNPROCESSABLE_CONTENT, check_response_code = 423, XDM_CONST.HTTP_RSP_CODE_LOCKED, check_response_code = 424, XDM_CONST.HTTP_RSP_CODE_FAILED_DEPENDENCY, check_response_code = 425, XDM_CONST.HTTP_RSP_CODE_TOO_EARLY, check_response_code = 426, XDM_CONST.HTTP_RSP_CODE_UPGRADE_REQUIRED, check_response_code = 428, XDM_CONST.HTTP_RSP_CODE_PRECONDITION_REQUIRED, check_response_code = 429, XDM_CONST.HTTP_RSP_CODE_TOO_MANY_REQUESTS, check_response_code = 431, XDM_CONST.HTTP_RSP_CODE_REQUEST_HEADER_FIELDS_TOO_LARGE, check_response_code = 451, XDM_CONST.HTTP_RSP_CODE_UNAVAILABLE_FOR_LEGAL_REASONS, check_response_code = 500, XDM_CONST.HTTP_RSP_CODE_INTERNAL_SERVER_ERROR, check_response_code = 501, XDM_CONST.HTTP_RSP_CODE_NOT_IMPLEMENTED, check_response_code = 502, XDM_CONST.HTTP_RSP_CODE_BAD_GATEWAY, check_response_code = 503, XDM_CONST.HTTP_RSP_CODE_SERVICE_UNAVAILABLE, check_response_code = 504, XDM_CONST.HTTP_RSP_CODE_GATEWAY_TIMEOUT, check_response_code = 505, XDM_CONST.HTTP_RSP_CODE_HTTP_VERSION_NOT_SUPPORTED, check_response_code = 506, XDM_CONST.HTTP_RSP_CODE_VARIANT_ALSO_NEGOTIATES, check_response_code = 507, XDM_CONST.HTTP_RSP_CODE_INSUFFICIENT_STORAGE, check_response_code = 508, XDM_CONST.HTTP_RSP_CODE_LOOP_DETECTED, check_response_code = 511, XDM_CONST.HTTP_RSP_CODE_NETWORK_AUTHENTICATION_REQUIRED, check_response_code = null, null, to_string(check_response_code)),

| alter 
    xdm.network.tls.protocol_version = tmp_tls_version,
    xdm.network.http.response_code = set_response_code,
    xdm.source.sent_bytes = to_integer(requestBytes_s),
    xdm.target.sent_bytes = to_integer(responseBytes_s),
    xdm.network.application_protocol = requestProtocol_s,
    xdm.network.tls.cipher = securityCipher_s,
    xdm.network.http.referrer = referer_s,
    xdm.network.http.method = httpMethod_s,
    xdm.event.duration = to_integer(coalesce(multiply(to_float(timeTaken_d),1000), multiply(to_float(timeTaken_s),1000), multiply(to_float(TimeTaken),1000), to_integer(DurationMs))),
    xdm.source.user_agent = coalesce(userAgent , userAgent_s),
    xdm.source.location.country = clientCountry_s ,
    xdm.network.rule = routingRuleName_s,
    xdm.target.ipv4 = target_ipv4, 
    xdm.target.ipv6 = target_ipv6,
    xdm.network.http.url = originUrl_s ,
    xdm.target.host.fqdn = sni_s,
    xdm.target.domain = coalesce(sni_s ,trim_domain ),
    xdm.network.http.domain = coalesce(sni_s ,trim_domain ),
    xdm.intermediate.host.hostname = endpoint_s,
    xdm.target.host.hostname = if(originName_s = "N/A", "The request was served from the Azure Front Door cache", originName_s ),
    xdm.event.outcome = error_information_outcome , 
    xdm.event.outcome_reason = error_information_outcome_reason;



/*
Handles Application Gateway logs
*/
[RULE: application_gateway_access_and_waf_logs]
alter 
	get_properties_action = properties -> action,
	get_properties_httpStatus = to_integer(properties -> httpStatus),
	get_properties_host = properties -> host,
	get_properties_hostname = properties -> hostname,
	get_properties_originalHost = properties -> originalHost,
	get_properties_httpMethod = properties -> httpMethod,
	get_properties_requestQuery = properties -> requestQuery,
	get_properties_requestUri = properties -> requestUri,
	get_properties_originalRequestUriWithArgs = properties -> originalRequestUriWithArgs,
	get_properties_clientip = if(to_string(properties) ~= "clientIp", properties -> clientIp, to_string(properties) ~= "clientIP", properties -> clientIP),
	get_resultSignature_resCode = if(resultSignature ~= "^\d{3}$", to_integer(resultSignature)),
	get_callerIpAddress_ipv4 = if(callerIpAddress ~= "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}", arrayindex(regextract(callerIpAddress, "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}"), 0)),
	get_callerIpAddress_ipv6 = if(callerIpAddress ~= "(?:[a-fA-F\d]{0,4}\:){1,7}[a-fA-F\d]{0,4}", arrayindex(regextract(callerIpAddress, "(?:[a-fA-F\d]{0,4}\:){1,7}[a-fA-F\d]{0,4}"), 0))
| alter
	get_properties_clientip_ipv4 = if(get_properties_clientip ~= "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}", arrayindex(regextract(get_properties_clientip, "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}"), 0)),
	get_properties_clientip_ipv6 = if(get_properties_clientip ~= "(?:[a-fA-F\d]{0,4}\:){1,7}[a-fA-F\d]{0,4}", get_properties_clientip),
	get_properties_host_ipv4 = if(get_properties_host ~= "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}", arrayindex(regextract(get_properties_host, "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}"), 0)),
	get_properties_host_ipv6 = if(get_properties_host ~= "(?:[a-fA-F\d]{0,4}\:){1,7}[a-fA-F\d]{0,4}", get_properties_host),
	get_properties_host_name = if(get_properties_host ~= "[a-zA-Z0-9]", get_properties_host),
	get_properties_hostname_ipv4 = if(get_properties_hostname ~= "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}", arrayindex(regextract(get_properties_hostname, "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}"), 0)),
	get_properties_hostname_ipv6 = if(get_properties_hostname ~= "(?:[a-fA-F\d]{0,4}\:){1,7}[a-fA-F\d]{0,4}", get_properties_hostname),
	get_properties_hostname_name = if(get_properties_hostname ~= "[a-zA-Z0-9]", get_properties_hostname),
	get_properties_originalHost_ipv4 = if(get_properties_originalHost ~= "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}", arrayindex(regextract(get_properties_originalHost, "\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}"), 0)),
	get_properties_originalHost_ipv6 = if(get_properties_originalHost ~= "(?:[a-fA-F\d]{0,4}\:){1,7}[a-fA-F\d]{0,4}", get_properties_originalHost),
	get_properties_originalHost_name = if(get_properties_originalHost ~= "[a-zA-Z0-9]", get_properties_originalHost)
| alter
	check_response_code = coalesce(get_properties_httpStatus, get_resultSignature_resCode),
	check_duration = coalesce(to_integer(properties -> timeTaken), to_integer(durationMs)),
	check_hostname = arraydistinct(arraycreate(get_properties_host_name, get_properties_hostname_name, get_properties_originalHost_name))
| alter
	xdm.observer.action = get_properties_action,
    xdm.event.outcome_reason = if(resultType !~= "\d+", resultType, resultType ~= "\d+" and resultDescription != null, concat(resultDescription, " Error Code: ", resultType), concat("Error Code: ", resultType)),
	xdm.event.outcome = if(get_properties_action ~= "lock", XDM_CONST.OUTCOME_FAILED, get_properties_action = "Allowed", XDM_CONST.OUTCOME_SUCCESS, get_properties_action ~= "Matched|Detected", XDM_CONST.OUTCOME_PARTIAL, resultType ~= "Start", XDM_CONST.OUTCOME_UNKNOWN, resultType ~= "[Pp]rogress", XDM_CONST.OUTCOME_UNKNOWN, resultType ~= "Succe", XDM_CONST.OUTCOME_SUCCESS, resultType ~= "Fail", XDM_CONST.OUTCOME_FAILED, resultType ~= "Active", XDM_CONST.OUTCOME_UNKNOWN, resultType ~= "Resolv", XDM_CONST.OUTCOME_SUCCESS, resultType = null, null, to_string(resultType)),
	xdm.network.http.response_code = if(check_response_code = 100, XDM_CONST.HTTP_RSP_CODE_CONTINUE, check_response_code = 101, XDM_CONST.HTTP_RSP_CODE_SWITCHING_PROTOCOLS, check_response_code = 102, XDM_CONST.HTTP_RSP_CODE_PROCESSING, check_response_code = 103, XDM_CONST.HTTP_RSP_CODE_EARLY_HINTS, check_response_code = 200, XDM_CONST.HTTP_RSP_CODE_OK, check_response_code = 201, XDM_CONST.HTTP_RSP_CODE_CREATED, check_response_code = 202, XDM_CONST.HTTP_RSP_CODE_ACCEPTED, check_response_code = 203, XDM_CONST.HTTP_RSP_CODE_NON__AUTHORITATIVE_INFORMATION, check_response_code = 204, XDM_CONST.HTTP_RSP_CODE_NO_CONTENT, check_response_code = 205, XDM_CONST.HTTP_RSP_CODE_RESET_CONTENT, check_response_code = 206, XDM_CONST.HTTP_RSP_CODE_PARTIAL_CONTENT, check_response_code = 207, XDM_CONST.HTTP_RSP_CODE_MULTI__STATUS, check_response_code = 208, XDM_CONST.HTTP_RSP_CODE_ALREADY_REPORTED, check_response_code = 226, XDM_CONST.HTTP_RSP_CODE_IM_USED, check_response_code = 300, XDM_CONST.HTTP_RSP_CODE_MULTIPLE_CHOICES, check_response_code = 301, XDM_CONST.HTTP_RSP_CODE_MOVED_PERMANENTLY, check_response_code = 302, XDM_CONST.HTTP_RSP_CODE_FOUND, check_response_code = 303, XDM_CONST.HTTP_RSP_CODE_SEE_OTHER, check_response_code = 304, XDM_CONST.HTTP_RSP_CODE_NOT_MODIFIED, check_response_code = 305, XDM_CONST.HTTP_RSP_CODE_USE_PROXY, check_response_code = 307, XDM_CONST.HTTP_RSP_CODE_TEMPORARY_REDIRECT, check_response_code = 308, XDM_CONST.HTTP_RSP_CODE_PERMANENT_REDIRECT, check_response_code = 400, XDM_CONST.HTTP_RSP_CODE_BAD_REQUEST, check_response_code = 401, XDM_CONST.HTTP_RSP_CODE_UNAUTHORIZED, check_response_code = 402, XDM_CONST.HTTP_RSP_CODE_PAYMENT_REQUIRED, check_response_code = 403, XDM_CONST.HTTP_RSP_CODE_FORBIDDEN, check_response_code = 404, XDM_CONST.HTTP_RSP_CODE_NOT_FOUND, check_response_code = 405, XDM_CONST.HTTP_RSP_CODE_METHOD_NOT_ALLOWED, check_response_code = 406, XDM_CONST.HTTP_RSP_CODE_NOT_ACCEPTABLE, check_response_code = 407, XDM_CONST.HTTP_RSP_CODE_PROXY_AUTHENTICATION_REQUIRED, check_response_code = 408, XDM_CONST.HTTP_RSP_CODE_REQUEST_TIMEOUT, check_response_code = 409, XDM_CONST.HTTP_RSP_CODE_CONFLICT, check_response_code = 410, XDM_CONST.HTTP_RSP_CODE_GONE, check_response_code = 411, XDM_CONST.HTTP_RSP_CODE_LENGTH_REQUIRED, check_response_code = 412, XDM_CONST.HTTP_RSP_CODE_PRECONDITION_FAILED, check_response_code = 413, XDM_CONST.HTTP_RSP_CODE_CONTENT_TOO_LARGE, check_response_code = 414, XDM_CONST.HTTP_RSP_CODE_URI_TOO_LONG, check_response_code = 415, XDM_CONST.HTTP_RSP_CODE_UNSUPPORTED_MEDIA_TYPE, check_response_code = 416, XDM_CONST.HTTP_RSP_CODE_RANGE_NOT_SATISFIABLE, check_response_code = 417, XDM_CONST.HTTP_RSP_CODE_EXPECTATION_FAILED, check_response_code = 421, XDM_CONST.HTTP_RSP_CODE_MISDIRECTED_REQUEST, check_response_code = 422, XDM_CONST.HTTP_RSP_CODE_UNPROCESSABLE_CONTENT, check_response_code = 423, XDM_CONST.HTTP_RSP_CODE_LOCKED, check_response_code = 424, XDM_CONST.HTTP_RSP_CODE_FAILED_DEPENDENCY, check_response_code = 425, XDM_CONST.HTTP_RSP_CODE_TOO_EARLY, check_response_code = 426, XDM_CONST.HTTP_RSP_CODE_UPGRADE_REQUIRED, check_response_code = 428, XDM_CONST.HTTP_RSP_CODE_PRECONDITION_REQUIRED, check_response_code = 429, XDM_CONST.HTTP_RSP_CODE_TOO_MANY_REQUESTS, check_response_code = 431, XDM_CONST.HTTP_RSP_CODE_REQUEST_HEADER_FIELDS_TOO_LARGE, check_response_code = 451, XDM_CONST.HTTP_RSP_CODE_UNAVAILABLE_FOR_LEGAL_REASONS, check_response_code = 500, XDM_CONST.HTTP_RSP_CODE_INTERNAL_SERVER_ERROR, check_response_code = 501, XDM_CONST.HTTP_RSP_CODE_NOT_IMPLEMENTED, check_response_code = 502, XDM_CONST.HTTP_RSP_CODE_BAD_GATEWAY, check_response_code = 503, XDM_CONST.HTTP_RSP_CODE_SERVICE_UNAVAILABLE, check_response_code = 504, XDM_CONST.HTTP_RSP_CODE_GATEWAY_TIMEOUT, check_response_code = 505, XDM_CONST.HTTP_RSP_CODE_HTTP_VERSION_NOT_SUPPORTED, check_response_code = 506, XDM_CONST.HTTP_RSP_CODE_VARIANT_ALSO_NEGOTIATES, check_response_code = 507, XDM_CONST.HTTP_RSP_CODE_INSUFFICIENT_STORAGE, check_response_code = 508, XDM_CONST.HTTP_RSP_CODE_LOOP_DETECTED, check_response_code = 511, XDM_CONST.HTTP_RSP_CODE_NETWORK_AUTHENTICATION_REQUIRED, check_response_code = null, null, to_string(check_response_code)),
	xdm.event.duration = check_duration,
	xdm.source.ipv4 = coalesce(get_properties_clientip_ipv4, get_callerIpAddress_ipv4),
	xdm.source.ipv6	= coalesce(get_properties_clientip_ipv6, get_callerIpAddress_ipv6),
	xdm.source.host.ipv4_addresses = arraydistinct(arraycreate(get_properties_host_ipv4, get_properties_hostname_ipv4, get_properties_originalHost_ipv4)),
	xdm.source.host.ipv6_addresses = arraydistinct(arraycreate(get_properties_host_ipv6, get_properties_hostname_ipv6, get_properties_originalHost_ipv6)),
	xdm.source.host.hostname = arrayindex(check_hostname, 0),
	xdm.source.cloud.project = Tenant,
	xdm.intermediate.host.hostname = properties -> instanceId,
	xdm.target.url = coalesce(get_properties_originalRequestUriWithArgs, get_properties_requestUri),
	xdm.network.rule = to_string(object_create("ruleSetType", properties -> ruleSetType, "ruleSetVersion", properties -> ruleSetVersion, "ruleId", properties -> ruleId, "ruleGroup", properties -> ruleGroup)),
	xdm.event.description = to_string(properties -> details{}),
	xdm.alert.description = properties -> message,
	xdm.event.id = properties -> transactionId,
	xdm.observer.type = if(properties -> engine = "Azwaf", "MICROSOFT.NETWORK/APPLICATIONGATEWAYS"),
	xdm.source.port = to_integer(properties -> clientPort),
	xdm.network.http.method = if(get_properties_httpMethod = "ACL", XDM_CONST.HTTP_METHOD_ACL, get_properties_httpMethod = "BASELINE_CONTROL", XDM_CONST.HTTP_METHOD_BASELINE_CONTROL, get_properties_httpMethod = "BIND", XDM_CONST.HTTP_METHOD_BIND, get_properties_httpMethod = "CHECKIN", XDM_CONST.HTTP_METHOD_CHECKIN, get_properties_httpMethod = "CHECKOUT", XDM_CONST.HTTP_METHOD_CHECKOUT, get_properties_httpMethod = "CONNECT", XDM_CONST.HTTP_METHOD_CONNECT, get_properties_httpMethod = "COPY", XDM_CONST.HTTP_METHOD_COPY, get_properties_httpMethod = "DELETE", XDM_CONST.HTTP_METHOD_DELETE, get_properties_httpMethod = "GET", XDM_CONST.HTTP_METHOD_GET, get_properties_httpMethod = "HEAD", XDM_CONST.HTTP_METHOD_HEAD, get_properties_httpMethod = "LABEL", XDM_CONST.HTTP_METHOD_LABEL, get_properties_httpMethod = "LINK", XDM_CONST.HTTP_METHOD_LINK, get_properties_httpMethod = "LOCK", XDM_CONST.HTTP_METHOD_LOCK, get_properties_httpMethod = "MERGE", XDM_CONST.HTTP_METHOD_MERGE, get_properties_httpMethod = "MKACTIVITY", XDM_CONST.HTTP_METHOD_MKACTIVITY, get_properties_httpMethod = "MKCALENDAR", XDM_CONST.HTTP_METHOD_MKCALENDAR, get_properties_httpMethod = "MKCOL", XDM_CONST.HTTP_METHOD_MKCOL, get_properties_httpMethod = "MKREDIRECTREF", XDM_CONST.HTTP_METHOD_MKREDIRECTREF, get_properties_httpMethod = "MKWORKSPACE", XDM_CONST.HTTP_METHOD_MKWORKSPACE, get_properties_httpMethod = "MOVE", XDM_CONST.HTTP_METHOD_MOVE, get_properties_httpMethod = "OPTIONS", XDM_CONST.HTTP_METHOD_OPTIONS, get_properties_httpMethod = "ORDERPATCH", XDM_CONST.HTTP_METHOD_ORDERPATCH, get_properties_httpMethod = "PATCH", XDM_CONST.HTTP_METHOD_PATCH, get_properties_httpMethod = "POST", XDM_CONST.HTTP_METHOD_POST, get_properties_httpMethod = "PRI", XDM_CONST.HTTP_METHOD_PRI, get_properties_httpMethod = "PROPFIND", XDM_CONST.HTTP_METHOD_PROPFIND, get_properties_httpMethod = "PROPPATCH", XDM_CONST.HTTP_METHOD_PROPPATCH, get_properties_httpMethod = "PUT", XDM_CONST.HTTP_METHOD_PUT, get_properties_httpMethod = "REBIND", XDM_CONST.HTTP_METHOD_REBIND, get_properties_httpMethod = "REPORT", XDM_CONST.HTTP_METHOD_REPORT, get_properties_httpMethod = "SEARCH", XDM_CONST.HTTP_METHOD_SEARCH, get_properties_httpMethod = "TRACE", XDM_CONST.HTTP_METHOD_TRACE, get_properties_httpMethod = "UNBIND", XDM_CONST.HTTP_METHOD_UNBIND, get_properties_httpMethod = "UNCHECKOUT", XDM_CONST.HTTP_METHOD_UNCHECKOUT, get_properties_httpMethod = "UNLINK", XDM_CONST.HTTP_METHOD_UNLINK, get_properties_httpMethod = "UNLOCK", XDM_CONST.HTTP_METHOD_UNLOCK, get_properties_httpMethod = "UPDATE", XDM_CONST.HTTP_METHOD_UPDATE, get_properties_httpMethod = "UPDATEREDIRECTREF", XDM_CONST.HTTP_METHOD_UPDATEREDIRECTREF, get_properties_httpMethod = "VERSION_CONTROL", XDM_CONST.HTTP_METHOD_VERSION_CONTROL, get_properties_httpMethod = null, null, to_string(get_properties_httpMethod)),
	xdm.target.resource.type = if(get_properties_requestQuery = null, null, get_properties_requestQuery ~= "^\s*$", null, "requestQuery"),
	xdm.target.resource.value = if(get_properties_requestQuery = null, null, get_properties_requestQuery ~= "^\s*$", null, get_properties_requestQuery),
	xdm.source.user_agent = properties -> userAgent,
	xdm.network.http.content_type = properties -> contentType,
	xdm.target.sent_bytes = to_integer(properties -> receivedBytes),
	xdm.source.sent_bytes = to_integer(properties -> sentBytes),
	xdm.network.session_id = to_string(properties -> connectionSerialNumber),
	xdm.observer.name = coalesce(properties -> WAFMode, arrayindex(regextract(resourceId, "/([^\/]+)$"), -1)),
	xdm.observer.unique_identifier = properties -> WAFPolicyID,
	xdm.network.tls.cipher = properties -> sslCipher,
	xdm.network.tls.protocol_version = properties -> sslProtocol,
	xdm.network.tls.client_certificate.issuer = properties -> sslClientCertificateIssuerName;



[MODEL: dataset=msft_azure_waf_raw]
filter Category = "FrontDoorAccessLog"
| call msft_azure_resourcelogs_common_fields
| call front_door_common_fields
| call front_door_access_log;


filter Category = "FrontDoorWebApplicationFirewallLog"
| call msft_azure_resourcelogs_common_fields
| call front_door_common_fields
| call front_door_waf_log ;


filter Category in ("ApplicationGatewayAccessLog", "ApplicationGatewayFirewallLog")
| call msft_azure_resourcelogs_common_fields
| call application_gateway_access_and_waf_logs;