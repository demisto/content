[MODEL: dataset="kiteworks_kiteworks_raw"]
filter _raw_log ~= "^(?:\S+\s+){5}\{" // JSON Format 
| alter 
    syslog_priority = to_integer(arrayindex(regextract(_raw_log, "^\<(\d{1,3})\>\s*\w+"), 0)),
    syslog_prefix = split(arrayindex(regextract(_raw_log, "\<\d+\>((?:\S+\s*){5})") , 0), " "),
    json_payload = arrayindex(regextract(_raw_log, "(?:\S+\s+){5}(.+)"), 0)
| alter 
    syslog_hostname = arrayindex(syslog_prefix, 3),
    process_name = rtrim(arrayindex(syslog_prefix, 4), ":")
| alter syslog_facility = floor(divide(syslog_priority, 8))
| alter syslog_severity = to_string(subtract(syslog_priority, multiply(syslog_facility, 8)))
| alter
    agent_id = json_payload -> agent.id,
    agent_name = json_payload -> agent.name,
    app_host = json_payload -> app_host,
    //application = json_payload -> application,
    attachments = json_payload -> data.attachments[],
    client_device = json_payload -> client_device,
    client_id = json_payload -> client_id,
    client_name = json_payload -> client_name,
    context = json_payload -> data.context,
    data_type = json_payload -> data.type,
    description = json_payload -> description,
    email = json_payload -> data.email,
    email_id = json_payload -> data.mail.id,
    email_sender = json_payload -> data.mail.sender,
    email_subject = json_payload -> data.mail.subject,
    email_recipients = arraymap(json_payload -> data.recipients[], "@element" -> name),
    error_msg = json_payload -> data.error_msg,
    event = json_payload -> event,
    file_file_id = json_payload -> data.file.file_id,
    file_hash = json_payload -> data.file.hash,
    file_hash_algo = json_payload -> data.file.hash_algo, // example "sha3-256"
    file_id = json_payload -> data.file.id,
    file_owner_id = json_payload -> file_owner.id,
    file_owner_name = json_payload -> file_owner.name,
    file_path = json_payload -> data.file.path, 
    folder_name = coalesce(json_payload -> data.folder.name, json_payload -> data.dest_folder.name, json_payload -> data.source_folder.name, json_payload -> parent_folder.name),
    folder_path = coalesce(json_payload -> data.folder.path, json_payload -> data.dest_folder.path, json_payload -> data.source_folder.path, json_payload -> parent_folder.path), 
    full_log = json_payload -> full_log,
    host_id = json_payload -> data.host_id,
    id = json_payload -> id,
    is_folder_upload = json_payload -> data.is_folder_upload,
    is_malicious = json_payload -> data.malicious,
    is_successful = json_payload -> successful,
    mime = coalesce(json_payload -> data.mime, json_payload -> data.file.mime),
    resource_name = json_payload -> data.name,
    node_ip = json_payload -> data.node_ip,
    prohibited = json_payload -> data.prohibited,
    quarantined = json_payload -> data.quarantined,
    report_name = json_payload -> data.report_name,
    role_name = json_payload -> data.role_name,
    rule_id = json_payload -> rule.id,
    rule_name = json_payload -> rule.name,
    scanning_type = json_payload -> data.scanning_type,
    // service_name = json_payload -> data.service_name,
    session_id = json_payload -> data.session,
    file_size = json_payload -> data.file.size, 
    skipped = json_payload -> data.skipped,
    source_file_name = json_payload -> data.source_file.name,
    source_file_path = json_payload -> data.source_file.path,
    src_ip = coalesce(json_payload -> data.srcip, json_payload -> user_ip),
    status_reason = json_payload -> data.status_reason,
    sw_version = json_payload -> data.sw_version,
    syscheck_gname_after = json_payload -> syscheck.gname_after,
    syscheck_path = json_payload -> syscheck.path,
    syscheck_symbolic_path = json_payload -> syscheck.symbolic_path,
    syscheck_uid_after = json_payload -> syscheck.uid_after,
    syscheck_uname_after = json_payload -> syscheck.uname_after,
    target_client_id = json_payload -> data.client_id,
    target_user_guid = json_payload -> data.user.guid,
    target_user_id = json_payload -> data.user.id,
    target_user_name = json_payload -> data.user.name,
    target_users_names = arraystring(arraymap(json_payload -> data.users[], "@element" -> name), ","),
    tenant_id = json_payload -> tenant_id,
    token = json_payload -> token,
    // token_type = json_payload -> data.token_type,
    url_host = json_payload -> url_host,
    user_agent = json_payload -> user_agent,
    user_id = json_payload -> user_id,
    user_name = coalesce(json_payload -> user_name, json_payload -> data.srcuser, json_payload -> data.dstuser),
    user_type = json_payload -> user_type 
| alter 
    event_log_level = if(syslog_severity = "0", XDM_CONST.LOG_LEVEL_EMERGENCY, syslog_severity = "1", XDM_CONST.LOG_LEVEL_ALERT, syslog_severity = "2", XDM_CONST.LOG_LEVEL_CRITICAL, syslog_severity = "3", XDM_CONST.LOG_LEVEL_ERROR, syslog_severity = "4", XDM_CONST.LOG_LEVEL_WARNING, syslog_severity = "5", XDM_CONST.LOG_LEVEL_NOTICE, syslog_severity = "6", XDM_CONST.LOG_LEVEL_INFORMATIONAL, syslog_severity = "7", XDM_CONST.LOG_LEVEL_DEBUG, syslog_severity),
    src_ipv4 = if(src_ip ~= "(?:\d{1,3}\.){3}\d{1,3}", src_ip),
    src_ipv6 = if(src_ip ~= "(?:[a-fA-F\d]{0,4}\:){7}[\wa-fA-F]{0,4}", src_ip),
    node_ipv4 = if(node_ip ~= "(?:\d{1,3}\.){3}\d{1,3}", node_ip),
    node_ipv6 = if(node_ip ~= "(?:[a-fA-F\d]{0,4}\:){7}[\wa-fA-F]{0,4}", node_ip),
    file_hash_sha256 = if(file_hash_algo ~= "(?i)sha.+256", file_hash),
    file_hash_md5 = if(file_hash_algo ~= "(?i)md5", file_hash),
    operation_type = if(is_folder_upload = "1", XDM_CONST.OPERATION_TYPE_DIR_CREATE),
    verdict = if(prohibited in ("1", "true"), "prohibited", quarantined in("1", "true"), "quarantined", skipped in ("1", "true"), "skipped"),
    target_client_name = if(target_client_id != null, resource_name)
| alter 
    // xdm.auth.auth_method = token_type,
    xdm.alert.severity = syslog_severity,
    xdm.email.recipients = if(array_length(email_recipients) > 0, email_recipients),
    xdm.email.sender = email_sender,
    xdm.email.subject = email_subject,
    xdm.email.message_id = email_id,
    xdm.email.attachment.filename = arraystring(attachments, ","),
    xdm.event.description = coalesce(full_log, description),
    xdm.event.log_level = event_log_level,
    xdm.event.operation = operation_type,
    xdm.event.outcome = if(is_malicious in ("1", "true"), "MALICIOUS", is_successful = "1", XDM_CONST.OUTCOME_SUCCESS, is_successful = 0, XDM_CONST.OUTCOME_FAILED),
    xdm.event.outcome_reason = coalesce(error_msg, status_reason),
    xdm.event.type = event,
    xdm.intermediate.process.name = process_name,
    xdm.network.session_id = coalesce(session_id, context),
    xdm.network.rule = coalesce(rule_name, rule_id),
    xdm.observer.name = syslog_hostname,
    xdm.observer.version = sw_version,
    xdm.observer.action = verdict, 
    xdm.session_context_id = context,
    xdm.source.agent.type = scanning_type,
    xdm.source.agent.identifier = concat(agent_name, " (", agent_id, ")"),
    xdm.source.application.name = concat(client_id, " (", client_name, ")"),
    xdm.source.host.hostname = if(client_device != "None", client_device),
    xdm.source.ipv4 = src_ipv4,
    xdm.source.ipv6 = src_ipv6,
    xdm.source.user_agent = user_agent,
    xdm.source.user.identifier = coalesce(user_id, syscheck_uid_after),
    xdm.source.user.user_type = user_type, 
    xdm.source.user.username = coalesce(user_name, syscheck_uname_after),
    xdm.source.user.groups = if(syscheck_gname_after != null, arraycreate(syscheck_gname_after)),
    xdm.target.application.version = sw_version,
    xdm.target.file.directory = coalesce(folder_name),
    xdm.target.file.file_type = mime,
    xdm.target.file.filename = resource_name,
    xdm.target.file.path = coalesce(file_path, folder_path, syscheck_path, syscheck_symbolic_path), 
    xdm.target.host.device_id = coalesce(host_id, tenant_id),
    xdm.target.host.hostname = coalesce(app_host, url_host),
    xdm.target.ipv4 = node_ipv4,
    xdm.target.ipv6 = node_ipv6,
    xdm.target.file.size = to_integer(file_size),
    xdm.target.application.name = concat(target_client_id, " (", target_client_name, ")"),
    xdm.target.resource.id = coalesce(id, target_user_guid, file_file_id, file_id),
    xdm.target.resource.name = coalesce(name, rule_name, role_name, report_name),
    xdm.target.resource.type = data_type,
    xdm.target.resource.value = coalesce(token),
    xdm.target.resource.parent_id = coalesce(file_owner_id, file_owner_name),
    xdm.target.file_before.path = source_file_path, 
    xdm.target.file_before.filename = source_file_name,
    xdm.target.user.identifier = target_user_id,
    xdm.target.file.sha256 = file_hash_sha256,
    xdm.target.file.md5 = file_hash_md5,
    xdm.target.user.username = coalesce(target_user_name, target_users_names, email, file_owner_name);