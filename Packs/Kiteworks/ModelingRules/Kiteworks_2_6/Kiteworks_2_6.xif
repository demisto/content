[MODEL: dataset="kiteworks_kiteworks_raw"]
/*************************/
/* Embedded JSON Format  */ 
/*************************/
filter _raw_log ~= "^(?:\S+\s+){5}\{" 
| alter // extract syslog message parts 
    syslog_priority = to_integer(arrayindex(regextract(_raw_log, "^\<(\d{1,3})\>\s*\w+"), 0)),
    syslog_hostname = arrayindex(regextract(_raw_log, "(?:\S+\s+){3}(\S+)"), 0),
    syslog_process_id = arrayindex(regextract(_raw_log, "^(?:\S+\s+){4}[\w\-\.\:]+\[(\d+)"), 0),
    syslog_process_name = rtrim(arrayindex(regextract(_raw_log, "(?:\S+\s+){4}(\S+)"), 0), ":"),
    json_payload = arrayindex(regextract(_raw_log, "(?:\S+\s+){5}(\{.+)"), 0)
| alter json_data = json_payload -> data{}
| alter syslog_facility = floor(divide(syslog_priority, 8))
| alter syslog_severity = to_string(subtract(syslog_priority, multiply(syslog_facility, 8)))
| alter // extract json payload fields 
    agent_id = to_string(json_payload -> agent.id),
    agent_name = json_payload -> agent.name,
    app_host = json_payload -> app_host,
    client_device = json_payload -> client_device,
    client_id = to_string(json_payload -> client_id),
    client_name = json_payload -> client_name,
    context = to_string(json_data -> context),
    data_type = json_data -> type,
    description = json_payload -> description,
    email = json_data -> email,
    email_attachments = json_data -> attachments[],
    email_id = to_string(json_data -> mail.id),
    email_recipients = arraymap(json_data -> recipients[], "@element" -> name),
    email_sender = json_data -> mail.sender,
    email_subject = json_data -> mail.subject,
    error_msg = json_data -> error_msg,
    event = json_payload -> event,
    file_file_id = to_string(json_data -> file.file_id),
    file_hash = json_data -> file.hash,
    file_hash_algo = json_data -> file.hash_algo,
    file_id = to_string(json_data -> file.id),
    file_mime_type = coalesce(json_data -> mime, json_data -> file.mime),
    file_name = coalesce(json_data -> file.name, json_data -> source_file.name),
    file_owner_id = to_string(json_data -> file_owner.id),
    file_owner_name = json_data -> file_owner.name,
    file_path = coalesce(json_data -> file.path, json_data -> source_file.path),
    file_size = json_data -> file.size,
    file_fingerprints = json_data -> file.fingerprints[],
    folder_name = coalesce(json_data -> folder.name, json_data -> dest_folder.name, json_data -> source_folder.name, json_data -> parent_folder.name),
    folder_path = coalesce(json_data -> folder.path, json_data -> dest_folder.path, json_data -> source_folder.path, json_data -> parent_folder.path),
    full_log = json_payload -> full_log,
    host_id = to_string(json_data -> host_id),
    id = to_string(json_payload -> id),
    is_malicious = to_string(json_data -> malicious),
    is_prohibited = to_string(json_data -> prohibited),
    is_quarantined = to_string(json_data -> quarantined),
    is_skipped = to_string(json_data -> skipped),
    is_successful = to_string(json_payload -> successful),
    node_ip = json_data -> node_ip,
    report_name = json_data -> report_name,
    resource_name = json_data -> name,
    role_name = json_data -> role_name,
    rule_id = to_string(json_payload -> rule.id),
    rule_name = json_payload -> rule.name,
    scanning_type = json_data -> scanning_type,
    session_id = to_string(json_data -> session),
    src_ip = coalesce(json_data -> srcip, json_payload -> user_ip),
    status_reason = json_data -> status_reason,
    sw_version = json_data -> sw_version,
    syscheck_gname_after = json_payload -> syscheck.gname_after,
    syscheck_path = json_payload -> syscheck.path,
    syscheck_symbolic_path = json_payload -> syscheck.symbolic_path,
    syscheck_uid_after = to_string(json_payload -> syscheck.uid_after),
    syscheck_uname_after = json_payload -> syscheck.uname_after,
    target_client_id = to_string(json_data -> client_id),
    target_user_guid = to_string(json_data -> user.guid),
    target_user_id = to_string(json_data -> user.id),
    target_user_name = json_data -> user.name,
    target_users_names = arraystring(arraymap(json_data -> users[], "@element" -> name), ","),
    tenant_id = to_string(json_payload -> tenant_id),
    token = json_payload -> token,
    url_host = json_payload -> url_host,
    user_agent = json_payload -> user_agent,
    user_id = to_string(json_payload -> user_id),
    user_name = coalesce(json_payload -> user_name, json_data -> srcuser, json_data -> dstuser),
    user_type = json_payload -> user_type
| alter // addition post-extraction processing 
    email_attachments_filename = arraystring(arraymap(email_attachments, "@element" -> name), ","),
    email_attachments_extension = arraystring(arraymap(email_attachments, arrayindex(regextract("@element" -> name, "\.(\w+)"), 0)), ","),
    email_attachments_mime_type = arraystring(arraymap(email_attachments, "@element" -> mime), ","),
    email_attachments_md5 = arraymap(email_attachments, if("@element" -> hash_algo = "md5", "@element" -> hash)),
    email_attachments_sha256 = arraymap(email_attachments, if("@element" -> hash_algo ~= "(?i)sha.+256", "@element" -> hash)),
    email_attachments_size = to_integer(arrayindex(email_attachments, 0) -> size),
    file_hash_md5 = if(file_hash_algo ~= "(?i)md5", file_hash),
    file_hash_sha256 = if(file_hash_algo ~= "(?i)sha.+256", file_hash),
    file_fingerprints_md5 = arrayindex(arraymap(file_fingerprints , if("@element" -> algo = "md5", "@element" -> hash)), 0),
    file_fingerprints_sha256 = arrayindex(arraymap(file_fingerprints , if("@element" -> algo ~= "(?i)sha.+256", "@element" -> hash)), 0),
    node_ipv4 = if(node_ip ~= "(?:\d{1,3}\.){3}\d{1,3}", node_ip),
    node_ipv6 = if(node_ip ~= "(?:[a-fA-F\d]{0,4}\:){7}[\wa-fA-F]{0,4}", node_ip),
    src_ipv4 = if(src_ip ~= "(?:\d{1,3}\.){3}\d{1,3}", src_ip),
    src_ipv6 = if(src_ip ~= "(?:[a-fA-F\d]{0,4}\:){7}[\wa-fA-F]{0,4}", src_ip),
    target_client_name = if(target_client_id != null, resource_name),
    verdict = if(is_malicious = "1", "MALICIOUS", is_prohibited = "1", "PROHIBITED", is_quarantined = "1", "QUARANTINED", is_skipped = "1", "SKIPPED")
| alter // extract inner array of attachments fingerprints from the email attachments array 
    attachments_md5_fingerprints = arraymap(email_attachments, arraystring(arrayfilter("@element" -> fingerprints[], "@element" -> algo = "md5"), ",")),
    attachments_sha256_fingerprints = arraymap(email_attachments, arraystring(arrayfilter("@element" -> fingerprints[], "@element" -> algo ~= "sha.*256"), ","))
| alter // extract just the hashes out of the fingerprint object array 
    attachments_md5_hashes = arraymap(attachments_md5_fingerprints, "@element" -> hash),
    attachments_sha256_hashes = arraymap(attachments_sha256_fingerprints, "@element" -> hash)
| alter // xdm mapping
    xdm.alert.severity = syslog_severity,
    xdm.email.attachment.extension = email_attachments_extension, 
    xdm.email.attachment.filename = email_attachments_filename,
    xdm.email.attachment.file_type = email_attachments_mime_type, 
    xdm.email.attachment.md5 = arraystring(arraydistinct(arrayconcat(attachments_md5_hashes, email_attachments_md5)), ","),
    xdm.email.attachment.sha256 = arraystring(arraydistinct(arrayconcat(attachments_sha256_hashes, email_attachments_sha256)), ","),
    xdm.email.attachment.size = email_attachments_size,
    xdm.email.message_id = email_id,
    xdm.email.recipients = if(array_length(email_recipients) > 0, email_recipients),
    xdm.email.sender = email_sender,
    xdm.email.subject = email_subject,
    xdm.event.description = coalesce(full_log, description),
    xdm.event.log_level = if(syslog_severity = "0", XDM_CONST.LOG_LEVEL_EMERGENCY, syslog_severity = "1", XDM_CONST.LOG_LEVEL_ALERT, syslog_severity = "2", XDM_CONST.LOG_LEVEL_CRITICAL, syslog_severity = "3", XDM_CONST.LOG_LEVEL_ERROR, syslog_severity = "4", XDM_CONST.LOG_LEVEL_WARNING, syslog_severity = "5", XDM_CONST.LOG_LEVEL_NOTICE, syslog_severity = "6", XDM_CONST.LOG_LEVEL_INFORMATIONAL, syslog_severity = "7", XDM_CONST.LOG_LEVEL_DEBUG, syslog_severity),
    xdm.event.operation_sub_type = event,
    xdm.event.outcome = if(is_successful = "1", XDM_CONST.OUTCOME_SUCCESS, is_successful = "0", XDM_CONST.OUTCOME_FAILED),
    xdm.event.outcome_reason = coalesce(error_msg, status_reason),
    xdm.event.type = coalesce(event, syslog_process_name),
    xdm.network.rule = concat(rule_id, " (", rule_name, ")"),
    xdm.network.session_id = coalesce(session_id, context),
    xdm.observer.action = verdict,
    xdm.observer.name = syslog_hostname,
    xdm.observer.version = sw_version,
    xdm.session_context_id = context,
    xdm.source.agent.identifier = concat(agent_id, " (", agent_name, ")"),
    xdm.source.agent.type = scanning_type,
    xdm.source.application.name = concat(client_id, " (", client_name, ")"),
    xdm.source.host.hostname = if(client_device != "None", client_device),
    xdm.source.ipv4 = src_ipv4,
    xdm.source.ipv6 = src_ipv6,
    xdm.source.process.name = syslog_process_name,
    xdm.source.process.pid = to_integer(syslog_process_id),
    xdm.source.user_agent = user_agent,
    xdm.source.identity.groups = if(syscheck_gname_after != null, arraycreate(syscheck_gname_after)),
    xdm.source.identity.identifier = coalesce(user_id, syscheck_uid_after),
    xdm.source.identity.user_type = user_type,
    xdm.source.identity.username = coalesce(user_name, syscheck_uname_after),
    xdm.target.application.name = concat(target_client_id, " (", target_client_name, ")"),
    xdm.target.application.version = sw_version,
    xdm.target.file.directory = folder_name,
    xdm.target.file.file_type = file_mime_type,
    xdm.target.file.filename = coalesce(file_name, if(event ~= "file|upload" or description ~= "file", resource_name)),
    xdm.target.file.md5 = coalesce(file_hash_md5, file_fingerprints_md5),
    xdm.target.file.path = coalesce(file_path, folder_path, syscheck_path, syscheck_symbolic_path),
    xdm.target.file.sha256 = coalesce(file_hash_sha256, file_fingerprints_sha256),
    xdm.target.file.size = to_integer(file_size),
    xdm.target.host.device_id = coalesce(host_id, tenant_id),
    xdm.target.host.hostname = coalesce(app_host, url_host),
    xdm.target.ipv4 = node_ipv4,
    xdm.target.ipv6 = node_ipv6,
    xdm.target.resource.id = coalesce(id, target_user_guid, file_file_id, file_id),
    xdm.target.resource.name = coalesce(resource_name, rule_name, role_name, report_name),
    xdm.target.resource.parent_id = coalesce(file_owner_id, file_owner_name),
    xdm.target.resource.type = data_type,
    xdm.target.resource.value = token,
    xdm.target.identity.identifier = target_user_id,
    xdm.target.identity.username = coalesce(target_user_name, target_users_names, email, file_owner_name);

/*********************************/
/* Single-Line Key-Value Format  */ 
/*********************************/
filter _raw_log !~= "^(?:\S+\s+){5}\{" and _raw_log not contains "Activity Type:"
| alter  // extract syslog message parts 
    syslog_priority = to_integer(arrayindex(regextract(_raw_log, "^\<(\d{1,3})\>\s*\w+"), 0)),
    syslog_hostname = arrayindex(regextract(_raw_log, "(?:\S+\s+){3}(\S+)"), 0),
    syslog_process_id = arrayindex(regextract(_raw_log, "^(?:\S+\s+){4}[\w\-\.\:]+\[(\d+)"), 0),
    syslog_process_name = rtrim(arrayindex(regextract(_raw_log, "(?:\S+\s+){4}([\w\-\.\:]+)"), 0), ":"),
    syslog_payload = arrayindex(regextract(_raw_log, "(?:\S+\s+){5}(.+)"), 0)
| alter syslog_facility = floor(divide(syslog_priority, 8))
| alter syslog_severity = to_string(subtract(syslog_priority, multiply(syslog_facility, 8)))
| alter // extract fields 
    activity_type = arrayindex(regextract(syslog_payload, "type=(\S+)"), 0),
    audit_uid = arrayindex(regextract(syslog_payload, "auid=(\d+)"), 0),
    auth_service = arrayindex(regextract(syslog_payload, "grantors=(\S+)"), 0),
    command_name = trim(arrayindex(regextract(syslog_payload, "comm=(\S+)"), 0), "\""),
    effective_group_id = arrayindex(regextract(syslog_payload, "egid=(\d+)"), 0),
    effective_uid = arrayindex(regextract(syslog_payload, "euid=(\d+)"), 0),
    email_cipher =  arrayindex(regextract(syslog_payload, "cipher=([\w\-]+)"), 0),
    email_msg_id = arrayindex(regextract(syslog_payload, "^(\w+)\:\s*(?:from|to)"), 0),
    email_recipient = arrayindex(regextract(syslog_payload, "[\s,]to=\<?([\w\.\-\@]+)"), 0),
    email_relay_hostname = rtrim(arrayindex(regextract(syslog_payload, "relay=\[?([\w\-\@\.]+)"), 0), "\."),
    email_relay_ip = arrayindex(regextract(syslog_payload, "relay=(?:\S+)?\s*\[([^\+\]]+)\]"), 0),
    email_sender = arrayindex(regextract(syslog_payload, "from=\<?([\w\.\-\@]+)"), 0),
    email_tls_version =  arrayindex(regextract(syslog_payload, "version=([^,]+),"), 0),
    event_id = arrayindex(regextract(syslog_payload, "audit\(\d+[^\:]+\:(\d+)"), 0),
    executable = trim(arrayindex(regextract(syslog_payload, "(?:comm|exe|cmd|proctitle)=(\S+)"), 0), "\""),
    exec_args = arraystring(regextract(_raw_log, "a\d+=\"([^\"]+)"), " "),
    group_id = arrayindex(regextract(syslog_payload, "\s+gid=(\d+)"), 0),
    hostname = arrayindex(regextract(syslog_payload, "hostname=([\w\.\-]+)"), 0),
    ip_address = arrayindex(regextract(syslog_payload, "addr=([\d\.\:a-fA-F]+)"), 0),
    is_successful = arrayindex(regextract(syslog_payload, "success=(\w+)"), 0), 
    msg = rtrim(arrayindex(regextract(syslog_payload, "msg=\'(.+)"), 0), "\'"),
    name = trim(arrayindex(regextract(syslog_payload, "name=(\S+)"), 0), "\""),
    node = arrayindex(regextract(syslog_payload, "node=(\S+)"), 0),
    o_uid = arrayindex(regextract(syslog_payload, "ouid=(\d+)"), 0),
    objtype = arrayindex(regextract(syslog_payload, "objtype=(\S+)"), 0),
    operation = arrayindex(regextract(syslog_payload, "op=(\S+)"), 0),
    pid = arrayindex(regextract(syslog_payload, "\s+pid=(\d+)"), 0),
    ppid = arrayindex(regextract(syslog_payload, "ppid=(\d+)"), 0),
    result = arrayindex(regextract(syslog_payload, "res=(\w+)"), 0),
    session_id = arrayindex(regextract(syslog_payload, "ses=(\S+)"), 0),
    set_group_id = arrayindex(regextract(syslog_payload, "sgid=(\d+)"), 0),
    syscall_number = arrayindex(regextract(syslog_payload, "syscall=(\d+)"), 0),
    user_id = arrayindex(regextract(syslog_payload, "\s+uid=(\d+)"), 0),
    user_account = arrayindex(regextract(syslog_payload, "acct=\"?([^\"]+)"), 0),
    working_directory = arrayindex(regextract(syslog_payload, "cwd=\"?([^\"]+)"), 0)
| alter // post-extraction processing 
    process_cmd = coalesce(executable, exec_args, command_name),
    ipv4 = if(ip_address ~= "(?:\d{1,3}\.){3}\d{1,3}", ip_address),
    ipv6 = if(ip_address ~= "(?:[a-fA-F\d]{0,4}\:){7}[\wa-fA-F]{0,4}", ip_address),
    email_relay_ipv4 = if(email_relay_ip ~= "(?:\d{1,3}\.){3}\d{1,3}", email_relay_ip),
    email_relay_ipv6 = if(email_relay_ip ~= "(?:[a-fA-F\d]{0,4}\:){7}[\wa-fA-F]{0,4}", email_relay_ip)
| alter // xdm mapping 
    xdm.alert.severity = syslog_severity,
    xdm.auth.service = auth_service,
    xdm.email.recipients = if(email_recipient != null, arraycreate(email_recipient)), 
    xdm.email.message_id = email_msg_id,
    xdm.event.description = coalesce(msg, syslog_payload),
    xdm.event.id = event_id,
    xdm.event.log_level = if(syslog_severity = "0", XDM_CONST.LOG_LEVEL_EMERGENCY, syslog_severity = "1", XDM_CONST.LOG_LEVEL_ALERT, syslog_severity = "2", XDM_CONST.LOG_LEVEL_CRITICAL, syslog_severity = "3", XDM_CONST.LOG_LEVEL_ERROR, syslog_severity = "4", XDM_CONST.LOG_LEVEL_WARNING, syslog_severity = "5", XDM_CONST.LOG_LEVEL_NOTICE, syslog_severity = "6", XDM_CONST.LOG_LEVEL_INFORMATIONAL, syslog_severity = "7", XDM_CONST.LOG_LEVEL_DEBUG, syslog_severity),
    xdm.event.operation = operation,
    xdm.event.operation_sub_type = activity_type,
    xdm.event.outcome = if(is_successful = "yes" or result = "success", XDM_CONST.OUTCOME_SUCCESS, is_successful = "no" or result ~= "fail", XDM_CONST.OUTCOME_FAILED),
    xdm.email.sender = email_sender,
    xdm.event.type = coalesce(activity_type, syslog_process_name),
    xdm.intermediate.host.hostname = email_relay_hostname,
    xdm.intermediate.ipv4 = email_relay_ipv4,
    xdm.intermediate.ipv6 = email_relay_ipv6,
    xdm.network.session_id = session_id,
    xdm.network.tls.cipher = email_cipher,
    xdm.network.tls.protocol_version = if(email_tls_version ~= "(?i)TLS|SSL", email_tls_version),
    xdm.observer.name = syslog_hostname,
    xdm.session_context_id = session_id,
    xdm.source.host.hostname = coalesce(node, syslog_hostname),
    xdm.source.ipv4 = ipv4,
    xdm.source.ipv6 = ipv6,
    xdm.source.process.name = syslog_process_name,
    xdm.source.process.pid = to_integer(syslog_process_id),
    xdm.source.identity.groups = if(group_id != null, arraycreate(group_id)),
    xdm.source.identity.identifier = coalesce(audit_uid, user_id),
    xdm.source.identity.username = user_account,
    xdm.target.host.hostname = hostname, 
    xdm.target.ipv4 = ipv4,
    xdm.target.ipv6 = ipv6,
    xdm.target.process.command_line = process_cmd,
    xdm.target.process.name = coalesce(command_name, syscall_number), 
    xdm.target.process.pid = to_integer(pid),
    xdm.target.process.parent_id = ppid, 
    xdm.target.process.executable.path = working_directory,
    xdm.target.resource.name = if(name != "?", name),
    xdm.target.resource.type = objtype,
    xdm.target.identity.groups = arraydistinct(arraycreate(set_group_id, effective_group_id)),
    xdm.target.identity.identifier = coalesce(effective_uid, o_uid, user_id, audit_uid),
    xdm.target.identity.username = user_account;

/******************************************/
/* Activity-Group & Activity-Type Format  */ 
/******************************************/
filter _raw_log !~= "^(?:\S+\s+){5}\{" and _raw_log contains "Activity Type:"
| alter  // extract syslog message parts 
    syslog_priority = to_integer(arrayindex(regextract(_raw_log, "^\<(\d{1,3})\>\s*\w+"), 0)),
    syslog_hostname = arrayindex(regextract(_raw_log, "(?:\S+\s+){3}(\S+)"), 0),
    syslog_process_name = rtrim(arrayindex(regextract(_raw_log, "(?:\S+\s+){4}([\w\-\.\:]+)"), 0), ":"),
    syslog_payload = arrayindex(regextract(_raw_log, "(?:\S+\s+){5}(.+)"), 0)
| alter syslog_facility = floor(divide(syslog_priority, 8))
| alter syslog_severity = to_string(subtract(syslog_priority, multiply(syslog_facility, 8)))
| alter // extract fields 
    activity_type = arrayindex(regextract(syslog_payload, "Activity Type:\s*([^,]+)"), 0),
    activity_group = arrayindex(regextract(syslog_payload, "Activity Group:\s*([^,]+)"), 0),
    activity = arrayindex(regextract(syslog_payload, "Activity:\s*([^,]+)"), 0),
    server_ip = arrayindex(regextract(syslog_payload, "[:,]\s*([a-fA-F\d\.]+),\s*Activity"), 0),
    user_id = arrayindex(regextract(syslog_payload, "^\S+\s+id=(\d+)\s+\("), 0),
    username = arrayindex(regextract(syslog_payload, "^(\S+)\s+id=\d+\s+\("), 0)
| alter // post-extraction processing 
    src_ipv4 = if(server_ip ~= "(?:\d{1,3}\.){3}\d{1,3}", server_ip),
    src_ipv6 = if(server_ip ~= "(?:[a-fA-F\d]{0,4}\:){7}[\wa-fA-F]{0,4}", server_ip)
| alter // xdm mapping 
    xdm.alert.severity = syslog_severity,
    xdm.event.description = coalesce(activity, syslog_payload),
    xdm.event.log_level = if(syslog_severity = "0", XDM_CONST.LOG_LEVEL_EMERGENCY, syslog_severity = "1", XDM_CONST.LOG_LEVEL_ALERT, syslog_severity = "2", XDM_CONST.LOG_LEVEL_CRITICAL, syslog_severity = "3", XDM_CONST.LOG_LEVEL_ERROR, syslog_severity = "4", XDM_CONST.LOG_LEVEL_WARNING, syslog_severity = "5", XDM_CONST.LOG_LEVEL_NOTICE, syslog_severity = "6", XDM_CONST.LOG_LEVEL_INFORMATIONAL, syslog_severity = "7", XDM_CONST.LOG_LEVEL_DEBUG, syslog_severity),
    xdm.event.operation_sub_type = coalesce(activity_type, syslog_process_name),
    xdm.event.type = coalesce(activity_group, syslog_process_name),
    xdm.observer.name = syslog_hostname,
    xdm.source.host.hostname = syslog_hostname,
    xdm.source.ipv4 = src_ipv4,
    xdm.source.ipv6 = src_ipv6,
    xdm.source.process.name = syslog_process_name,
    xdm.source.process.command_line = if(syslog_process_name ~= "\.\w+", syslog_process_name),
    xdm.source.identity.identifier = user_id,
    xdm.source.identity.username = username;