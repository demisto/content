[MODEL: dataset=msft_graph_security_alerts_raw]
filter _reporting_device_name = "https://graph.microsoft.com/beta/security/alerts_v2"
| alter
    json_arr1 = evidence -> [0]{},
    json_arr2 = evidence -> [1]{},
    json_arr3 = evidence -> [2]{},
    json_arr4 = evidence -> [3]{},
    json_arr5 = evidence -> [4]{},
    json_arr6 = evidence -> [5]{},
    json_arr7 = evidence -> [6]{},
    json_arr8 = evidence -> [7]{},
    json_arr9 = evidence -> [8]{},
    json_arr10 = evidence -> [9]{}
| alter
    alertEvidence_json = if(json_arr1 ~= "alertEvidence", json_arr1, json_arr2 ~= "alertEvidence", json_arr2, json_arr3 ~= "alertEvidence", json_arr3, json_arr4 ~= "alertEvidence", json_arr4, json_arr5 ~= "alertEvidence", json_arr5, json_arr6 ~= "alertEvidence", json_arr6, json_arr7 ~= "alertEvidence", json_arr7, json_arr8 ~= "alertEvidence", json_arr8, json_arr9 ~= "alertEvidence", json_arr9, json_arr10 ~= "alertEvidence", json_arr10, null),
//
analyzedMessageEvidence_json = if(json_arr1 ~= "analyzedMessageEvidence", json_arr1, json_arr2 ~= "analyzedMessageEvidence", json_arr2, json_arr3 ~= "analyzedMessageEvidence", json_arr3, json_arr4 ~= "analyzedMessageEvidence", json_arr4, json_arr5 ~= "analyzedMessageEvidence", json_arr5, json_arr6 ~= "analyzedMessageEvidence", json_arr6, json_arr7 ~= "analyzedMessageEvidence", json_arr7, json_arr8 ~= "analyzedMessageEvidence", json_arr8, json_arr9 ~= "analyzedMessageEvidence", json_arr9, json_arr10 ~= "analyzedMessageEvidence", json_arr10, null),
//
cloudApplicationEvidence_json = if(json_arr1 ~= "cloudApplicationEvidence", json_arr1, json_arr2 ~= "cloudApplicationEvidence", json_arr2, json_arr3 ~= "cloudApplicationEvidence", json_arr3, json_arr4 ~= "cloudApplicationEvidence", json_arr4, json_arr5 ~= "cloudApplicationEvidence", json_arr5, json_arr6 ~= "cloudApplicationEvidence", json_arr6, json_arr7 ~= "cloudApplicationEvidence", json_arr7, json_arr8 ~= "cloudApplicationEvidence", json_arr8, json_arr9 ~= "cloudApplicationEvidence", json_arr9, json_arr10 ~= "cloudApplicationEvidence", json_arr10, null),
//
deviceEvidence_json = if(json_arr1 ~= "deviceEvidence", json_arr1, json_arr2 ~= "deviceEvidence", json_arr2, json_arr3 ~= "deviceEvidence", json_arr3, json_arr4 ~= "deviceEvidence", json_arr4, json_arr5 ~= "deviceEvidence", json_arr5, json_arr6 ~= "deviceEvidence", json_arr6, json_arr7 ~= "deviceEvidence", json_arr7, json_arr8 ~= "deviceEvidence", json_arr8, json_arr9 ~= "deviceEvidence", json_arr9, json_arr10 ~= "deviceEvidence", json_arr10, null),
//
fileEvidence_json = if(json_arr1 ~= "fileEvidence", json_arr1, json_arr2 ~= "fileEvidence", json_arr2, json_arr3 ~= "fileEvidence", json_arr3, json_arr4 ~= "fileEvidence", json_arr4, json_arr5 ~= "fileEvidence", json_arr5, json_arr6 ~= "fileEvidence", json_arr6, json_arr7 ~= "fileEvidence", json_arr7, json_arr8 ~= "fileEvidence", json_arr8, json_arr9 ~= "fileEvidence", json_arr9, json_arr10 ~= "fileEvidence", json_arr10, null),
//
ipEvidence_json = if(json_arr1 ~= "ipEvidence", json_arr1, json_arr2 ~= "ipEvidence", json_arr2, json_arr3 ~= "ipEvidence", json_arr3, json_arr4 ~= "ipEvidence", json_arr4, json_arr5 ~= "ipEvidence", json_arr5, json_arr6 ~= "ipEvidence", json_arr6, json_arr7 ~= "ipEvidence", json_arr7, json_arr8 ~= "ipEvidence", json_arr8, json_arr9 ~= "ipEvidence", json_arr9, json_arr10 ~= "ipEvidence", json_arr10, null),
//
mailboxEvidence_json = if(json_arr1 ~= "mailboxEvidence", json_arr1, json_arr2 ~= "mailboxEvidence", json_arr2, json_arr3 ~= "mailboxEvidence", json_arr3, json_arr4 ~= "mailboxEvidence", json_arr4, json_arr5 ~= "mailboxEvidence", json_arr5, json_arr6 ~= "mailboxEvidence", json_arr6, json_arr7 ~= "mailboxEvidence", json_arr7, json_arr8 ~= "mailboxEvidence", json_arr8, json_arr9 ~= "mailboxEvidence", json_arr9, json_arr10 ~= "mailboxEvidence", json_arr10, null),
//
processEvidence_json = if(json_arr1 ~= "processEvidence", json_arr1, json_arr2 ~= "processEvidence", json_arr2, json_arr3 ~= "processEvidence", json_arr3, json_arr4 ~= "processEvidence", json_arr4, json_arr5 ~= "processEvidence", json_arr5, json_arr6 ~= "processEvidence", json_arr6, json_arr7 ~= "processEvidence", json_arr7, json_arr8 ~= "processEvidence", json_arr8, json_arr9 ~= "processEvidence", json_arr9, json_arr10 ~= "processEvidence", json_arr10, null),
//
registryKeyEvidence_json = if(json_arr1 ~= "registryKeyEvidence", json_arr1, json_arr2 ~= "registryKeyEvidence", json_arr2, json_arr3 ~= "registryKeyEvidence", json_arr3, json_arr4 ~= "registryKeyEvidence", json_arr4, json_arr5 ~= "registryKeyEvidence", json_arr5, json_arr6 ~= "registryKeyEvidence", json_arr6, json_arr7 ~= "registryKeyEvidence", json_arr7, json_arr8 ~= "registryKeyEvidence", json_arr8, json_arr9 ~= "registryKeyEvidence", json_arr9, json_arr10 ~= "registryKeyEvidence", json_arr10, null),
//
registryValueEvidence_json = if(json_arr1 ~= "registryValueEvidence", json_arr1, json_arr2 ~= "registryValueEvidence", json_arr2, json_arr3 ~= "registryValueEvidence", json_arr3, json_arr4 ~= "registryValueEvidence", json_arr4, json_arr5 ~= "registryValueEvidence", json_arr5, json_arr6 ~= "registryValueEvidence", json_arr6, json_arr7 ~= "registryValueEvidence", json_arr7, json_arr8 ~= "registryValueEvidence", json_arr8, json_arr9 ~= "registryValueEvidence", json_arr9, json_arr10 ~= "registryValueEvidence", json_arr10, null),
//
urlEvidence_json = if(json_arr1 ~= "urlEvidence", json_arr1, json_arr2 ~= "urlEvidence", json_arr2, json_arr3 ~= "urlEvidence", json_arr3, json_arr4 ~= "urlEvidence", json_arr4, json_arr5 ~= "urlEvidence", json_arr5, json_arr6 ~= "urlEvidence", json_arr6, json_arr7 ~= "urlEvidence", json_arr7, json_arr8 ~= "urlEvidence", json_arr8, json_arr9 ~= "urlEvidence", json_arr9, json_arr10 ~= "urlEvidence", json_arr10, null)
| alter
// alertEvidence Fields
    alertEvidence_tags = json_extract_scalar(alertEvidence_json, "$.tags"),
// analyzedMessageEvidence Fields
    analyzedMessageEvidence_tags = json_extract_scalar(analyzedMessageEvidence_json, "$.tags"),
    analyzedMessageEvidence_threats = json_extract_scalar(analyzedMessageEvidence_json, "$.threats"),
    analyzedMessageEvidence_internetMessageId = json_extract_scalar(analyzedMessageEvidence_json, "$.internetMessageId"),
    analyzedMessageEvidence_receivedDateTime = json_extract_scalar(analyzedMessageEvidence_json, "$.receivedDateTime"),
    analyzedMessageEvidence_recipientEmailAddress = json_extract_scalar(analyzedMessageEvidence_json, "$.recipientEmailAddress"),
    analyzedMessageEvidence_subject = json_extract_scalar(analyzedMessageEvidence_json, "$.subject"),
// cloudApplicationEvidence Fields
    cloudApplicationEvidence_tags = json_extract_scalar(cloudApplicationEvidence_json, "$.tags"),
    cloudApplicationEvidence_displayName = json_extract_scalar(cloudApplicationEvidence_json, "$.displayName"),
    cloudApplicationEvidence_instanceName = json_extract_scalar(cloudApplicationEvidence_json, "$.instanceName"),
// deviceEvidence Fields
    deviceEvidence_tags = json_extract_scalar(deviceEvidence_json, "$.tags"),
    deviceEvidence_riskScore = json_extract_scalar(deviceEvidence_json, "$.riskScore"),
    deviceEvidence_deviceDnsName = json_extract_scalar(deviceEvidence_json, "$.deviceDnsName"),
    deviceEvidence_osPlatform = json_extract_scalar(deviceEvidence_json, "$.osPlatform"),
    deviceEvidence_version = json_extract_scalar(deviceEvidence_json, "$.version"),
// fileEvidence Fields
    fileEvidence_tags = json_extract_scalar(fileEvidence_json, "$.tags"),
    fileEvidence_detectionStatus = json_extract_scalar(fileEvidence_json, "$.detectionStatus"),
    fileEvidence_fileDetails_fileName = json_extract_scalar(fileEvidence_json, "$.fileDetails.fileName"),
    fileEvidence_fileDetails_filePath = json_extract_scalar(fileEvidence_json, "$.fileDetails.filePath"),
    fileEvidence_fileDetails_fileSize = json_extract_scalar(fileEvidence_json, "$.fileDetails.fileSize"),
    fileEvidence_fileDetails_sha256 = json_extract_scalar(fileEvidence_json, "$.fileDetails.sha256"),
    fileEvidence_fileDetails_signer = json_extract_scalar(fileEvidence_json, "$.fileDetails.signer"),
// ipEvidence Fields
    ipEvidence_tags = json_extract_scalar(ipEvidence_json, "$.tags"),
    ipEvidence_ipAddress = json_extract_scalar(ipEvidence_json, "$.ipAddress"),
// mailboxEvidence Fields
    mailboxEvidence_tags = json_extract_scalar(mailboxEvidence_json, "$.tags"),
    mailboxEvidence_displayName = json_extract_scalar(mailboxEvidence_json, "$.displayName"),
    mailboxEvidence_primaryAddress = json_extract_scalar(mailboxEvidence_json, "$.primaryAddress"),
    mailboxEvidence_userAccount_accountName = json_extract_scalar(mailboxEvidence_json, "$.userAccount.accountName"),
    mailboxEvidence_userAccount_userPrincipalName = json_extract_scalar(mailboxEvidence_json, "$.userAccount.userPrincipalName"),
    mailboxEvidence_userAccount_domainName = json_extract_scalar(mailboxEvidence_json, "$.userAccount.domainName"),
    mailboxEvidence_userAccount_userSid = json_extract_scalar(mailboxEvidence_json, "$.userAccount.userSid"),
// processEvidence Fields
    processEvidence_tags = json_extract_scalar(processEvidence_json, "$.tags"),
    processEvidence_detectionStatus = json_extract_scalar(processEvidence_json, "$.detectionStatus"),
    processEvidence_userAccount_accountName = json_extract_scalar(processEvidence_json, "$.userAccount.accountName"),
    processEvidence_userAccount_userPrincipalName = json_extract_scalar(processEvidence_json, "$.userAccount.userPrincipalName"),
    processEvidence_userAccount_domainName = json_extract_scalar(processEvidence_json, "$.userAccount.domainName"),
    processEvidence_userAccount_userSid = json_extract_scalar(processEvidence_json, "$.userAccount.userSid"),
    processEvidence_imageFile_fileName = json_extract_scalar(processEvidence_json, "$.imageFile.fileName"),
    processEvidence_imageFile_filePath = json_extract_scalar(processEvidence_json, "$.imageFile.filePath"),
    processEvidence_imageFile_fileSize = json_extract_scalar(processEvidence_json, "$.imageFile.fileSize"),
    processEvidence_imageFile_sha256 = json_extract_scalar(processEvidence_json, "$.imageFile.sha256"),
    processEvidence_imageFile_signer = json_extract_scalar(processEvidence_json, "$.imageFile.signer"),
    processEvidence_parentProcessId = json_extract_scalar(processEvidence_json, "$.parentProcessId"),
    processEvidence_processCommandLine = json_extract_scalar(processEvidence_json, "$.processCommandLine"),
    processEvidence_processId = json_extract_scalar(processEvidence_json, "$.processId"),
// registryKeyEvidence Fields
    registryKeyEvidence_tags = json_extract_scalar(registryKeyEvidence_json, "$.tags"),
    registryKeyEvidence_registryKey = json_extract_scalar(registryKeyEvidence_json, "$.registryKey"),
// registryKeyEvidence Fields    
    registryValueEvidence_tags = json_extract_scalar(registryValueEvidence_json, "$.tags"),
    registryValueEvidence_registryKey = json_extract_scalar(registryValueEvidence_json, "$.registryKey"),
    registryValueEvidence_registryValue = json_extract_scalar(registryValueEvidence_json, "$.registryValue"),
    registryValueEvidence_registryValueName = json_extract_scalar(registryValueEvidence_json, "$.registryValueName"),
    registryValueEvidence_registryValueType = json_extract_scalar(registryValueEvidence_json, "$.registryValueType"),
// urlEvidence Fields
    urlEvidence_tags = json_extract_scalar(urlEvidence_json, "$.tags"),
    urlEvidence_url = json_extract_scalar(urlEvidence_json, "$.url")
| alter
    check_resolved = if(to_string(status) ~= "resolved", to_boolean("TRUE"), to_boolean("FALSE")),
    unified_os_version = arraystring(arraycreate(deviceEvidence_osPlatform, deviceEvidence_version), " "),
    lowercase_os_platform = lowercase(deviceEvidence_osPlatform),
    lowercase_registry_type = lowercase(registryValueEvidence_registryValueType),
    get_email_timestamp = arraystring(regextract(analyzedMessageEvidence_receivedDateTime, "\d+\/\d+\/\d+ \d+:\d+:\d+ [A-Z]{2}"), ""),
    get_email_zone = replex(arraystring(regextract(analyzedMessageEvidence_receivedDateTime, "[+|-]\d+:\d+"), ""), ":", ""),
    check_ipv4 = if(ipEvidence_ipAddress !~= ":", ipEvidence_ipAddress, null),
    check_ipv6 = if(ipEvidence_ipAddress ~= ":", ipEvidence_ipAddress, null)
| alter
    xdm.observer.unique_identifier = id,
    xdm.event.description = title,
    xdm.event.is_completed = check_resolved,
    xdm.alert.subcategory = category,
    xdm.event.outcome = if(fileEvidence_detectionStatus = "blocked", XDM_CONST.OUTCOME_SUCCESS, fileEvidence_detectionStatus = "detected", XDM_CONST.OUTCOME_FAILED, fileEvidence_detectionStatus = "prevented", XDM_CONST.OUTCOME_PARTIAL, processEvidence_detectionStatus = "blocked", XDM_CONST.OUTCOME_SUCCESS, processEvidence_detectionStatus = "detected", XDM_CONST.OUTCOME_FAILED, processEvidence_detectionStatus = "prevented", XDM_CONST.OUTCOME_PARTIAL, classification = "falsePositive", XDM_CONST.OUTCOME_SUCCESS, classification = "truePositive", XDM_CONST.OUTCOME_FAILED, classification = "benignPositive", XDM_CONST.OUTCOME_PARTIAL, coalesce(fileEvidence_detectionStatus, processEvidence_detectionStatus, classification) = null, null, to_string(coalesce(fileEvidence_detectionStatus, processEvidence_detectionStatus, classification))),
    xdm.event.tags = arraycreate(coalesce(alertEvidence_tags, analyzedMessageEvidence_tags, cloudApplicationEvidence_tags, deviceEvidence_tags, fileEvidence_tags, ipEvidence_tags, mailboxEvidence_tags, processEvidence_tags, registryKeyEvidence_tags, registryValueEvidence_tags, urlEvidence_tags)),
    xdm.alert.severity = coalesce(severity, deviceEvidence_riskScore),
    xdm.source.host.device_id = tenantId,
    xdm.alert.description = description,
    xdm.intermediate.agent.identifier = detectorId,
    xdm.event.id = coalesce(incidentId, providerAlertId),
    xdm.intermediate.host.fqdn = coalesce(alertWebUrl, incidentWebUrl),
    xdm.alert.original_threat_name = coalesce(determination, analyzedMessageEvidence_threats),
    xdm.observer.type = serviceSource,
    xdm.intermediate.host.hostname = detectionSource,
    xdm.email.message_id = analyzedMessageEvidence_internetMessageId, 
    xdm.email.delivery_timestamp = parse_timestamp("%m/%d/%Y %H:%M:%S %p %z", arraystring(arraycreate(get_email_timestamp, get_email_zone), " ")),
    xdm.email.recipients = arraycreate(analyzedMessageEvidence_recipientEmailAddress),
    xdm.email.subject = analyzedMessageEvidence_subject,
    xdm.source.application.name = cloudApplicationEvidence_displayName,
    xdm.source.cloud.project = cloudApplicationEvidence_instanceName,
    xdm.source.host.fqdn = deviceEvidence_deviceDnsName,
    xdm.source.host.os = unified_os_version, 
    xdm.source.host.os_family = if(lowercase_os_platform ~= "win|windows", XDM_CONST.OS_FAMILY_WINDOWS, lowercase_os_platform ~= "mac|macos", XDM_CONST.OS_FAMILY_MACOS, lowercase_os_platform ~= "linux", XDM_CONST.OS_FAMILY_LINUX, lowercase_os_platform ~= "android", XDM_CONST.OS_FAMILY_ANDROID, lowercase_os_platform ~= "ios", XDM_CONST.OS_FAMILY_IOS, lowercase_os_platform ~= "ubuntu", XDM_CONST.OS_FAMILY_UBUNTU, lowercase_os_platform ~= "debian", XDM_CONST.OS_FAMILY_DEBIAN, lowercase_os_platform ~= "fedora", XDM_CONST.OS_FAMILY_FEDORA, lowercase_os_platform ~= "centos", XDM_CONST.OS_FAMILY_CENTOS, lowercase_os_platform ~= "chromeos", XDM_CONST.OS_FAMILY_CHROMEOS, lowercase_os_platform ~= "solaris", XDM_CONST.OS_FAMILY_SOLARIS, lowercase_os_platform ~= "scada", XDM_CONST.OS_FAMILY_SCADA),
    xdm.target.file.filename = fileEvidence_fileDetails_fileName,
    xdm.target.file.path = fileEvidence_fileDetails_filePath,
    xdm.target.file.size = to_integer(fileEvidence_fileDetails_fileSize),
    xdm.target.file.sha256 = fileEvidence_fileDetails_sha256,
    xdm.target.file.signer = fileEvidence_fileDetails_signer,
    xdm.alert.mitre_techniques = arraycreate(mitreTechniques),
    xdm.source.ipv4 = check_ipv4,
    xdm.source.ipv6 = check_ipv6,
    xdm.source.user.username = coalesce(mailboxEvidence_displayName, mailboxEvidence_primaryAddress, mailboxEvidence_userAccount_accountName, mailboxEvidence_userAccount_userPrincipalName, mailboxEvidence_userAccount_domainName, processEvidence_userAccount_accountName, processEvidence_userAccount_userPrincipalName),
    xdm.source.user.domain = coalesce(mailboxEvidence_userAccount_domainName, processEvidence_userAccount_domainName),
    xdm.source.user.identifier = coalesce(mailboxEvidence_userAccount_userSid, processEvidence_userAccount_userSid),
    xdm.source.process.executable.filename = processEvidence_imageFile_fileName,
    xdm.source.process.executable.path = processEvidence_imageFile_filePath,
    xdm.source.process.executable.size = to_integer(processEvidence_imageFile_fileSize), 
    xdm.source.process.executable.sha256 = processEvidence_imageFile_sha256,
    xdm.source.process.executable.signer = processEvidence_imageFile_signer,
    xdm.source.process.parent_id = processEvidence_parentProcessId,
    xdm.source.process.command_line = processEvidence_processCommandLine,
    xdm.source.process.pid = to_integer(processEvidence_processId),
    xdm.target.registry.key = coalesce(registryKeyEvidence_registryKey, registryValueEvidence_registryKey),
    xdm.target.registry.data = registryValueEvidence_registryValue,
    xdm.target.registry.value = registryValueEvidence_registryValueName,
    xdm.target.registry.value_type = if(lowercase_registry_type ~= "binary", XDM_CONST.REGISTRY_VALUE_TYPE_REG_BINARY, lowercase_registry_type ~= "dword$", XDM_CONST.REGISTRY_VALUE_TYPE_REG_DWORD, lowercase_registry_type ~= "dword_little", XDM_CONST.REGISTRY_VALUE_TYPE_REG_DWORD_LITTLE_ENDIAN, lowercase_registry_type ~= "dword_big", XDM_CONST.REGISTRY_VALUE_TYPE_REG_DWORD_BIG_ENDIAN, lowercase_registry_type ~= "expand", XDM_CONST.REGISTRY_VALUE_TYPE_REG_EXPAND_SZ, lowercase_registry_type ~= "link", XDM_CONST.REGISTRY_VALUE_TYPE_REG_LINK, lowercase_registry_type ~= "multi", XDM_CONST.REGISTRY_VALUE_TYPE_REG_MULTI_SZ, lowercase_registry_type ~= "none", XDM_CONST.REGISTRY_VALUE_TYPE_REG_NONE, lowercase_registry_type ~= "qword$", XDM_CONST.REGISTRY_VALUE_TYPE_REG_QWORD, lowercase_registry_type ~= "qword_little", XDM_CONST.REGISTRY_VALUE_TYPE_REG_QWORD_LITTLE_ENDIAN, lowercase_registry_type ~= "reg_sz", XDM_CONST.REGISTRY_VALUE_TYPE_REG_SZ, lowercase_registry_type = null, null, to_string(lowercase_registry_type)),
    xdm.target.url = urlEvidence_url;