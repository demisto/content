[MODEL: dataset = microsoft_windows_raw]
filter provider_name contains "Microsoft-Windows-Security-" or channel = "System" or channel = "Application" or provider_name = "Microsoft-Windows-PowerShell" or provider_name = "Microsoft-Windows-TaskScheduler" or provider_name = "Microsoft-Windows-Windows Firewall With Advanced Security"
| alter
        IpPort = json_extract_scalar(event_data ,"$.IpPort"),
        LogLevel = lowercase(log_level),
        Command_Path = arrayindex(regextract(message, "Command Path = (.*?)\s{2,}"), 0),
        Command_Name = arrayindex(regextract(message, "Command Name = (.*?)\s{2,}"),0),
        Script_Name = arrayindex(regextract(arrayindex(regextract(message, "Script Name = (.*?)\s{2,}"),0), "\\([^\\]+)$"), 0),
        Script_Path = arrayindex(regextract(message, "Script Name = (.*?)\s{2,}"),0),
        message_user_sid = arrayindex(regextract(message, "User \"([^\"]+)\""),0),
        message_task = arrayindex(regextract(message, "task \"([^\"]+)\""),0),
        message_instance = arrayindex(regextract(message, "instance \"([^\"]+)\""),0),
        message_processID = arrayindex(regextract(message, "process ID \"([^\"]+)\""),0),
        message_action = arrayindex(regextract(message, "action \"([^\"]+)\""),0),
        rule_name1 = arraystring(regextract(message, "Rule\s+Name\:\s+(.*)\b\s+Modifying\s*User:"), ""),
    	rule_name2 = arraystring(regextract(message, "Rule\s*Name\:\s+(.*[^\w\s])\s+Modifying\s*User"), "")
| alter
    	check_rule_name1 = if(rule_name1 ~= "\S", rule_name1, null),
    	check_rule_name2 = if(rule_name2 ~= "\S", rule_name2, null),
        check_Command_Path = if(Command_Path ~= "\S", Command_Path, null),
        check_Command_Name = if(Command_Name ~= "\S", Command_Name, null),
        check_Script_Name = if(Script_Name ~= "\S", Script_Name, null),
        check_Script_Path = if(Script_Path ~= "\S", Script_Path, null),
        check_message_user_sid = if(message_user_sid ~= "\S", message_user_sid, null),
        check_message_task = if(message_task ~= "\S", message_task, null),
        check_message_processID = if(message_processID ~= "\S", message_processID, null),
        check_message_action = if(message_action ~= "\S", message_action, null),
        check_fw_description = if(provider_name = "Microsoft-Windows-Windows Firewall With Advanced Security", arraystring(regextract(message, "^[^\.]+\."), ""), null),
        check_fw_sid = if(provider_name = "Microsoft-Windows-Windows Firewall With Advanced Security", arraystring(regextract(message, "Modifying\s*User\:\s+(.*)\b\s+Modifying\s+Application:"), ""), null),
        check_fw_process_name = if(provider_name = "Microsoft-Windows-Windows Firewall With Advanced Security", arraystring(regextract(message, "Modifying\s*Application:\s+\S+\\([^\.]+\.exe)"), ""), null),
        get_message_instance_exe_path = if(message_instance ~= "exe", message_instance, null),
        get_message_instance_exe_process = if(message_instance ~= "exe", arrayindex(regextract(message_instance, "\\([a-zA-Z0-9]+\.exe)$"),0), null)
| alter
        EventType = coalesce(event_action, task, check_message_task, "")      
| alter
        xdm.source.process.executable.directory = arraystring(regextract(message, "Modifying\s*Application:\s+(\S+)"), ""),
        xdm.network.rule = coalesce(check_rule_name1, check_rule_name2),
        xdm.source.ipv4 = coalesce(if(json_extract_scalar(event_data ,"$.IpAddress") contains "." and json_extract_scalar(event_data ,"$.IpAddress") not contains ":",json_extract_scalar(event_data ,"$.IpAddress"),json_extract_scalar(event_data ,"$.IpAddress") contains ":" and json_extract_scalar(event_data ,"$.IpAddress") contains ".", arrayindex(regextract(json_extract_scalar(event_data ,"$.IpAddress"),"(\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3})"),0),null),if(channel="System",json_extract_scalar(event_data ,"$.Ipaddress"))),
        xdm.source.ipv6 = if(json_extract_scalar(event_data ,"$.IpAddress") contains ":" and json_extract_scalar(event_data ,"$.IpAddress") not contains ".",json_extract_scalar(event_data ,"$.IpAddress"),json_extract_scalar(event_data ,"$.IpAddress") contains ":" and json_extract_scalar(event_data ,"$.IpAddress") contains ".", arrayindex(regextract(json_extract_scalar(event_data ,"$.IpAddress"),"(.*?)\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}"),0),null),
        xdm.source.port = if(IpPort~="\d+",to_integer(IpPort),null),
        xdm.source.user.username = coalesce(if(json_extract_scalar(event_data ,"$.SubjectUserName") not contains "*$",json_extract_scalar(event_data ,"$.SubjectUserName")),if(json_extract_scalar(user, "$.name") not contains "*$",json_extract_scalar(user, "$.name")),if(channel="Application" and json_extract_scalar(event_data ,"$.param3") contains "*\\*",json_extract_scalar(event_data ,"$.param3")),if(channel="Application",json_extract_scalar(event_data ,"$.User"))),
        xdm.source.process.executable.path = coalesce(get_message_instance_exe_path, json_extract_scalar(event_data ,"$.ProcessName"), if(channel="Application",json_extract_scalar(event_data ,"$.Module")), if(check_Script_Path != null and check_Script_Path != "", check_Script_Path), if(check_Command_Path != null and check_Command_Path != "", check_Command_Path), process_path),
        xdm.source.process.name = coalesce(check_fw_process_name, get_message_instance_exe_process, arrayindex(regextract(json_extract_scalar(event_data ,"$.ProcessName"),"\\([^\\]+)$"),0), if(channel="System",json_extract_scalar(event_data ,"$.ProcessName")), process_name),
        xdm.source.application.name = coalesce(if(channel="System", json_extract_scalar(event_data ,"$.param1")), arrayindex(regextract(message, "HostApplication=([\S]+)"), 0)),
        xdm.source.user.identifier = coalesce(check_message_user_sid, json_extract_scalar(event_data ,"$.SubjectUserSid"),json_extract_scalar(user ,"$.identifier"),json_extract_scalar(user,"$.SubjectUserSid"), check_fw_sid, if(channel="System",json_extract_scalar(event_data ,"$.UserSid"))),
        xdm.target.user.identifier = json_extract_scalar(event_data ,"$.TargetUserSid"),
        xdm.source.user.domain = coalesce(json_extract_scalar(event_data ,"$.SubjectDomainName"),json_extract_scalar(user_data,"$.SubjectDomainName"),json_extract_scalar(user,"$.domain")),
        xdm.target.user.domain = coalesce(json_extract_scalar(event_data ,"$.TargetDomainName"),if(channel="System",json_extract_scalar(event_data ,"$.DCName"))),
        xdm.target.user.username = if(json_extract_scalar(event_data ,"$.TargetUserName") not contains "*$" AND EventType!="Security Group Management",json_extract_scalar(event_data ,"$.TargetUserName")),
        xdm.target.user.groups = if(json_extract_scalar(event_data ,"$.TargetUserName") not contains "*$" AND EventType="Security Group Management",arraycreate(json_extract_scalar(event_data ,"$.TargetUserName"))),
        xdm.source.host.hostname = coalesce(host_name,computer_name,if(json_extract_scalar(event_data ,"$.SubjectUserName") contains "*$",json_extract_scalar(event_data ,"$.SubjectUserName") ),if(json_extract_scalar(user, "$.name") contains "*$",json_extract_scalar(user, "$.name")),if(json_extract_scalar(user_data,"$.SubjectUserName") contains "*$",json_extract_scalar(user_data,"$.SubjectUserName")),if(channel="System",json_extract_scalar(event_data ,"$.HostName")),if(channel="Application" and task="Devices",json_extract_scalar(event_data ,"$.param1"))),
        xdm.source.host.fqdn = json_extract_scalar(event_data ,"$.WorkstationName"),
        xdm.event.type = channel,
        xdm.event.id = to_string(event_id),
        xdm.observer.type = provider_name,
        xdm.event.log_level = if(LogLevel="information", XDM_CONST.LOG_LEVEL_INFORMATIONAL,LogLevel="error",XDM_CONST.LOG_LEVEL_ERROR, LogLevel="warning",XDM_CONST.LOG_LEVEL_WARNING, LogLevel="critical",XDM_CONST.LOG_LEVEL_CRITICAL,to_string(coalesce(opcode,log_level))),
        xdm.event.description = coalesce(message,if(channel="System",json_extract_scalar(event_data ,"$.updateTitle")),if(channel="Application",json_extract_scalar(event_data ,"$.param3"))),
        xdm.alert.description = if(channel="System",coalesce(json_extract_scalar(event_data ,"$.error"), check_fw_description != null, check_fw_description, json_extract_scalar(event_data ,"$.ErrorMessage"))),
        xdm.alert.original_alert_id = activity_id,
        xdm.source.process.pid = coalesce(to_integer(check_message_processID), to_integer(process_pid),if(channel="System",to_integer(json_extract_scalar(event_data ,"$.ProcessID")))),
        xdm.source.process.thread_id = to_integer(process_thread_id),
        xdm.session_context_id = to_string(record_id),
        xdm.target.ipv4 = "",
        xdm.target.port = to_integer(0),
        logonType = json_extract_scalar(event_data ,"$.LogonType"),
        userType = json_extract_scalar(user,"$.type"),
        xdm.source.host.os_family = XDM_CONST.OS_FAMILY_WINDOWS,
        xdm.event.operation_sub_type = coalesce(check_message_action, arrayindex(regextract(message,"(^.*?)\."),0),if(channel="System",json_extract_scalar(event_data ,"$.param2"))),
        xdm.source.host.os = os_subtype,
        xdm.event.original_event_type = EventType,
        xdm.source.process.command_line = coalesce(if(check_Command_Name != null and check_Command_Name != "", check_Command_Name), process_cmd),
        xdm.source.process.executable.md5 = process_md5,
        xdm.source.process.executable.sha256 = process_sha256,
        xdm.event.outcome=if(event_result = "success",XDM_CONST.OUTCOME_SUCCESS, event_result="failure",XDM_CONST.OUTCOME_FAILED, event_result),
        xdm.event.outcome_reason = if(channel="Application" and task="TM",json_extract_scalar(event_data ,"$.param2")),
        xdm.observer.unique_identifier = provider_guid,
        xdm.source.process.executable.filename = if(check_Script_Name != null and check_Script_Name != "", check_Script_Name, null)
| alter
        xdm.logon.type = if(logonType="2", XDM_CONST.LOGON_TYPE_INTERACTIVE,logonType="3",XDM_CONST.LOGON_TYPE_NETWORK, logonType="4", XDM_CONST.LOGON_TYPE_BATCH ,logonType="5",XDM_CONST.LOGON_TYPE_SERVICE , logonType ="6", XDM_CONST.LOGON_TYPE_PROXY , logonType="7", XDM_CONST.LOGON_TYPE_NEW_CREDENTIALS , logonType="8", XDM_CONST.LOGON_TYPE_NETWORK_CLEARTEXT, logonType="9",XDM_CONST.LOGON_TYPE_NEW_CREDENTIALS ,logonType="10",XDM_CONST.LOGON_TYPE_REMOTE_INTERACTIVE  ,logonType="11", XDM_CONST.LOGON_TYPE_CACHED_INTERACTIVE, logonType="12", XDM_CONST.LOGON_TYPE_CACHED_REMOTE_INTERACTIVE , logonType="13", XDM_CONST.LOGON_TYPE_CACHED_UNLOCK,logonType),
        xdm.source.user.user_type = if(userType contains "User",XDM_CONST.USER_TYPE_REGULAR, userType contains "Service", XDM_CONST.USER_TYPE_SERVICE_ACCOUNT , userType contains "Computer",XDM_CONST.USER_TYPE_MACHINE_ACCOUNT ,userType);