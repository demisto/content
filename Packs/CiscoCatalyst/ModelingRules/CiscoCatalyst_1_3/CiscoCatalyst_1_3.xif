[MODEL: dataset="cisco_catalyst_raw"]
alter seq_number = arrayindex(regextract(_raw_log , "(\d+)\:\s+\**[A-Za-z]+\s+\d+\s\d+\:\d+\:\d+\.\d+\s*[A-Z]*\:\s%[A-Z]+"),0),
    facility = arrayindex(regextract(_raw_log, "%([\-\_\w]+[\-\_\w]*)\-\d+"),0),
    severity = arrayindex(regextract(_raw_log ,"%[\-\_\w]+[\-\_\w]*\-(\d+)\-"),0),
    event_type = arrayindex(regextract(_raw_log, "[\-\_\w]+[\-\_\w]*\-\d+\-([\w\-\_]+)"),0),
    message1 = arrayindex(regextract(_raw_log,"%[\-\_\w]+[\-\_\w]*\-\d+\-[A-Z\-\_]+\:\s(.*)"),0),
    message2 = arrayindex(regextract(_raw_log,"\<\d+\>\d+\:\s(.*)"),0),
    switch_hostname = arrayindex(regextract(_raw_log ,"\<\d+\>\d+\:\s([\w+\-\_]+)\:\s\d+\:"),0)
| alter InterfaceName1 = arrayindex(regextract(_raw_log ,"Interface\s([^\/]+\/\d+\/*\d*)"),0),
    InterfaceName2 = arrayindex(regextract(_raw_log ,"interface\sname\s([^\/]+\/\d+\/\d+)"),0),
    InterfaceName3 = arrayindex(regextract(_raw_log , "Interface\s([A-Za-z\-\d]+)"),0),
    username1 = arrayindex(regextract(_raw_log ,"\[user\:\s([^\]]+)\]"),0),
    username2 = arrayindex(regextract(_raw_log ,"\:\sUser\s(\S+)\s"),0),
    username3 = arrayindex(regextract(_raw_log ,"\(\d+\.\d+\.\d+\.\d+\)\)\,\suser\s(\S+)"),0),
    sourceip1 =  arrayindex(regextract(_raw_log , "list\s\d+\spermitted\s(\d+\.\d+\.\d+\.\d+)"),0),
    sourceip2 = arrayindex(regextract(_raw_log ,"\[Source\:\s(\d+\.\d+\.\d+\.\d+)\]"),0),
    sourceip3 = arrayindex(regextract(_raw_log ,"session\s\d+\((\d+\.\d+\.\d+\.\d+)\)"),0),
    sourceip4 = arrayindex(regextract(_raw_log ,"connection\sfrom\s(\d+\.\d+\.\d+\.\d+)"),0),
    sourceip5 = arrayindex(regextract(_raw_log ,"to\shost\s(\d+\.\d+\.\d+\.\d+)\s"),0),
    sourceip6 = arrayindex(regextract(_raw_log ,"address\s(\d+\.\d+\.\d+\.\d+)\son"),0),
    sourceip7 = arrayindex(regextract(_raw_log ,"\((\d+\.\d+\.\d+\.\d+)\)\)\,\suser"),0),
    sourceport1 = arrayindex(regextract(_raw_log ,"\[localport\:\s(\d+)\]"),0),
    sourceport2 = arrayindex(regextract(_raw_log ,"to\shost\s\d+\.\d+\.\d+\.\d+\sport\s(\d+)"),0),
    processname1 = arrayindex(regextract(_raw_log ,"by\sprocess\s([A-Za-z\s]+)Process"),0),
    processname2 = arrayindex(regextract(_raw_log ,"by\sprocess\s([A-Za-z\s]+)Policy\smanager"),0),
    vlan = arrayindex(regextract(_raw_log ,"address\s\d+\.\d+\.\d+\.\d+\son\sVlan(\d+)"),0),
    sourceipv6 = arrayindex(regextract(_raw_log ,"\,\ssourced\sby\s(\S+)"),0),
    pid = arrayindex(regextract(_raw_log ,"\[PID\:([^\,]+)\,"),0),
    change_state = arrayindex(regextract(_raw_log, "\,\s(changed\sstate\sto[a-zA-Z\s]+)"),0)
| alter seq_number = replex(seq_number, "^0+","")
| alter xdm.network.session_id = seq_number,
    xdm.event.type = event_type,
    xdm.alert.severity = severity,
    xdm.observer.type = facility,
    xdm.event.description = coalesce(message1,message2),
    xdm.observer.name = switch_hostname,
    xdm.target.interface = coalesce(InterfaceName1, InterfaceName2 , InterfaceName3),
    xdm.source.ipv4 = coalesce(sourceip1 , sourceip2 ,sourceip3, sourceip4, sourceip5,sourceip6,sourceip7),
    xdm.source.user.username = coalesce(username1 ,username2, username3),
    xdm.source.port = to_integer(coalesce(sourceport1,sourceport2 )),
    xdm.source.process.name = coalesce(processname1 ,processname2 ),
    xdm.source.vlan = to_integer(vlan),
    xdm.source.ipv6 = sourceipv6,
    xdm.source.process.identifier = pid,
    xdm.observer.action = change_state,
    xdm.event.outcome = if(event_type contains "SUCCESS", XDM_CONST.OUTCOME_SUCCESS, event_type contains "FAIL",XDM_CONST.OUTCOME_FAILED, null);