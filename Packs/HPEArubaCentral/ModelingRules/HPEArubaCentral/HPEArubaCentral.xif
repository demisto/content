[MODEL: dataset = aruba_central_raw]
/*
Mapping Audit events
*/
filter ts != null and timestamp = null
| alter
    xdm.event.original_event_type = "Audit",
    xdm.event.type = classification ,
    xdm.event.description = description ,
    xdm.event.id = id ,
    xdm.source.ipv4 = if(is_ipv4(ip_addr) = TRUE, ip_addr),
    xdm.source.ipv6 = if(is_ipv6(ip_addr) = TRUE, ip_addr),
    xdm.source.user.upn = if(user contains "@", user, null),
    xdm.source.user.username =  if(user not contains "@", user, null),
    xdm.source.user.groups = arraycreate(gid),
    xdm.target.host.device_id = `target`;

/*
Mapping Network events
*/
filter ts = null and timestamp != null
| alter
    xdm.event.original_event_type = "Networking",
    xdm.event.type = event_type,
    xdm.event.description = description, 
    xdm.event.id = event_uuid,
    xdm.event.log_level = if(level ~= "INFO|Informational",XDM_CONST.LOG_LEVEL_INFORMATIONAL, level ~= "WARNING|Warning", XDM_CONST.LOG_LEVEL_WARNING, level ~= "EMERGENCY|Emergency", XDM_CONST.LOG_LEVEL_EMERGENCY,
    level ~= "ALERT|Alert",XDM_CONST.LOG_LEVEL_ALERT, level ~= "CRITICAL|Critical",XDM_CONST.LOG_LEVEL_CRITICAL,level ~= "ERROR|Error",XDM_CONST.LOG_LEVEL_ERROR,
    level~="NOTICE|Notice", XDM_CONST.LOG_LEVEL_NOTICE,level~= "DEBUG|Debug",XDM_CONST.LOG_LEVEL_DEBUG, level),
    xdm.source.host.hostname = hostname,
    xdm.source.host.mac_addresses = arraycreate(client_mac),
    xdm.source.user.groups = arraycreate(group_name),
    xdm.intermediate.host.mac_addresses = arraycreate(coalesce(device_mac, bssid)),
    xdm.intermediate.host.device_id = device_serial,
    xdm.intermediate.host.device_category = device_type;