[MODEL: dataset=msft_ecm_raw]
// timestamp part
 alter time = arrayindex(regextract(_raw_log, "time=\"(\d+\:\d+\:\d{2})"), 0),
    date = arrayindex(regextract(_raw_log, "date=\"(\d{1,2}\-\d{1,2}\-\d{4})"), 0),
    timestamp = arrayindex(regextract(_raw_log, "(\d{1,2}\-\d{1,2}\-\d{4}\s+\S{8})"), 0)
| alter dateTime = format_string("%s %s", date, time)
| alter timestamp = coalesce(timestamp, dateTime)
// end timestamp part
| alter thread_id = arrayindex(regextract(_raw_log, "thread\=\"*(\d+)\"*"), 0),
    file = arrayindex(regextract(_raw_log, "file\=\"([^\"]*)\""), 0),
    msg_1 = arrayindex(regextract(_raw_log, "^(.+)[\~|\$]"), 0),
    msg_2 = arrayindex(regextract(_raw_log, "^\<\!\[LOG\[(.+)\]LOG"), 0),
    component = arrayindex(regextract(_raw_log, "component\=\"(\S+)\""), 0),
    component2 = arrayindex(regextract(_raw_log, "\<(SMS\S+)\>"), 0),
    ipV4 = arrayindex(regextract(_raw_log, "\b(?:\d{1,3}\.){3}\d{1,3}\b"), 0),
    filename = arrayindex(regextract(_raw_log ,"Processing\sfile\s(\S+)\s"),0),
    path = arrayindex(regextract(_raw_log ,"path\s*[\:|\=]\s*\'([^\']+)\'"),0),
    dns_name = arrayindex(regextract(_raw_log ,"DNS\sname\s\=\s\'([^\']+)\'"),0),
    site_server = arrayindex(regextract(_raw_log , "SiteServer\=([^\,]+)\,"),0),
    duration1 = arrayindex(regextract(_raw_log ,"\s(\d*\.*\d+)\sseconds"),0),
    duration2 = arrayindex(regextract(_raw_log ,"\s(\d*\.*\d+)\shours"),0),
    duration3 = arrayindex(regextract(_raw_log ,"\s(\d+)\sminutes"),0),
    event_type = arrayindex(regextract(_raw_log ,"^([A-Za-z]*\s*[a-zA-Z]*)\>*\:"),0)
| alter duration = coalesce(duration1 , duration2, duration3 )
| alter component = coalesce(component, component2)
| alter msg = coalesce(msg_1, msg_2)
| alter
    xdm.session_context_id = thread_id,
    xdm.event.description = msg,
    xdm.event.operation = component,
    xdm.source.ipv4 = ipV4,
    xdm.target.file.filename = coalesce(filename,file),
    xdm.target.file.path = path,
    xdm.network.dns.dns_resource_record.name = dns_name,
    xdm.observer.name = site_server,
    xdm.event.duration = to_number(duration),
    xdm.event.type = event_type;